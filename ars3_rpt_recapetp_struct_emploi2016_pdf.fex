-* File ars3_rpt_recapetp_struct_emploi.fex
-*==================================================================================================
-* Projet Suivi d'activité 2014 - Tableau de bord : Récapitulatif des paramètres et Total ETP
-*
-* Destinataire  : Philippe Louvel
-* Auteur        : Nicolas Wattiaux (Information Builders), Guy Fute (Aixial BI)
-* Date création : 01/07/2015
-* Description   :
-*
-*--------------------------------------------------------------------------------------------------
-* Modifications
-*  Date   : jj/mm/ssaa
-*  Auteur : first last - sté
-*  Objet  : ...
-*==================================================================================================
SET SPACES =ON;
-DEFAULTH &AX04_LIBELLE = ''
-DEFAULTH &VALEUR_FINALE = 0
-SET &ID_INDICATEUR= 'I_GIDB0015';
-SET &EXERCICE=2015;
-*-INCLUDE ars3_DEFAUT_STAT
 
-*-SET &ECHO=ALL;
-DEFAULTH &ID_INDICATEUR='I_GIDB001C';
-DEFAULTH &ID_INDUCTEUR='I_GIDB0013';
-DEFAULTH &EXERCICE='2014';
 
-DEFAULTH &NOM_VUE = 'VUE_STATUT'
 
-*Filtre GEO
-DEFAULTH &GEO='1';
-DEFAULTH &GEO_NATIONAL='GEOA000';
-DEFAULTH &NEW_REGION='_FOC_NULL';
-DEFAULTH &REGION='_FOC_NULL';
-DEFAULTH &DEPARTEMENT='_FOC_NULL';
 
-*Filtre ENTITE
-DEFAULTH &ENTITE='1';
-DEFAULTH &ENTITE_NATIONAL='AARS16ENT1ARS00';
-DEFAULTH &ENTITE_TYPE='_FOC_NULL';
 
-*Filtre ACTIVITE
-DEFAULTH &ACTIVITE='1';
-DEFAULTH &ACTIVITE_NATIONAL='AARS1600';
-DEFAULTH &SECTEUR='_FOC_NULL';
-DEFAULTH &TYPE_INT='_FOC_NULL';
-DEFAULTH &MISSION='_FOC_NULL';
 
-*Filtre STATUT
-*-DEFAULTH &STATUT_NIV='1';
-DEFAULTH &STATUT_NATIONAL='AARS16ST00TOUT';
-DEFAULTH &STATUT='_FOC_NULL';
-DEFAULTH &EMPLOI='_FOC_NULL';
 
-*Filtre PLAFOND
-DEFAULTH &PLAFOND_NATIONAL='AARS15EFF0TOUS';
-DEFAULTH &PLAFOND='_FOC_NULL';
 
-*******Récupération des valeur de niveau pour chaque filtre
-DEFAULTH &STR_GEO='';
-DEFAULTH &STR_ENT='';
-DEFAULTH &STR_ACT='';
-DEFAULTH &STR_PLAF='';
-DEFAULTH &STR_STAT='';
 
SET CENT-ZERO=ON
SET CDN=ON
-SET &GEO = IF '&NEW_REGION' EQ 'undefined' OR 'TOUT' OR '_FOC_NULL' THEN '1' ELSE '2';
-SET &GEO = IF &REGION EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN &GEO ELSE '3';
 
-SET &NIV_ENTITE = IF &ENTITE_TYPE EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN '1' ELSE '2';
 
-SET &NIV_PLAFOND = IF &PLAFOND EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN '1' ELSE '2';
 
-SET &NIV_STATUT = IF '&STATUT' EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN '1' ELSE '2';
-SET &NIV_STATUT = IF &EMPLOI EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN &NIV_STATUT ELSE '3';
-SET &LIB_STATUT = IF &NIV_STATUT EQ '1' THEN 'Tous les statuts' ELSE 'Emplois';
 
-SET &NIV_ACTIVITE= IF '&SECTEUR' EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN '1' ELSE '2';
 
 
 
 
-*-DEFAULT &FMT_SORTIE='HTML'
 
-*---------------------------------------------------
-* Récupération des codes axes selon les paramètres
-*---------------------------------------------------
 
-*-INCLUDE ars3_READ_GEO_INDUCT
-* File ars3_read_geo_induct.fex
 
TABLE FILE AXE_GEOAR
PRINT VALEUR_AXE
WHERE NIVEAU EQ &GEO
-IF &GEO EQ 1 THEN GOTO GEO_FIN ELSE IF &GEO EQ 2 THEN GOTO :GEO_N2 ELSE IF &GEO EQ 3 THEN GOTO :GEO_N3;
-:GEO_N3
WHERE GEOAR_N3 EQ '&REGION'
-GOTO GEO_FIN
-:GEO_N2
WHERE GEOAR_N2 EQ '&NEW_REGION'
-GOTO GEO_FIN
-GEO_FIN
ON TABLE HOLD AS TMP_AXE_GEO FORMAT ALPHA
END
-RUN
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_GEO='';
-REPEAT :BOUCLE1 WHILE &IORETURN EQ 0;
-READ TMP_AXE_GEO &GEO_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE1;
-SET &STR_GEO=IF &STR_GEO EQ '' THEN &GEO_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_GEO | '''' | ',' | '''' | &GEO_VAL | '''' ELSE &STR_GEO | ',' | '''' | &GEO_VAL | '''';
-SET &STR_GEO=IF &LINES EQ 1 THEN '''' | &STR_GEO | '''' ELSE &STR_GEO;
-SET &CPT=&CPT+1;
-:BOUCLE1
-CLOSE TMP_AXE_GEO
-TYPE &STR_GEO
 
 
-*-INCLUDE ars3_READ_ACTIVITE_INDUC
 
TABLE FILE AXE_AARS16INDUCTEUR
PRINT VALEUR_AXE
WHERE NIVEAU EQ &NIV_ACTIVITE.QUOTEDSTRING
-IF &NIV_ACTIVITE EQ 1 THEN GOTO :ACTIVITE_N1;
WHERE AARS16IND_N2 EQ '&SECTEUR'
-:ACTIVITE_N1
ON TABLE HOLD AS TMP_AXE_AARS16INDUCTEUR FORMAT ALPHA
END
-RUN
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_ACT='';
-REPEAT :BOUCLE44 WHILE &IORETURN EQ 0;
-READ TMP_AXE_AARS16INDUCTEUR &ACT_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE44;
-SET &STR_ACT=IF &STR_ACT EQ '' THEN &ACT_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_ACT | '''' | ',' | '''' | &ACT_VAL | '''' ELSE &STR_ACT | ',' | '''' | &ACT_VAL | '''';
-SET &STR_ACT=IF &LINES EQ 1 THEN '''' | &STR_ACT | '''' ELSE &STR_ACT;
-SET &CPT=&CPT+1;
-:BOUCLE44
-CLOSE TMP_AXE_AARS16INDUCTEUR
-TYPE &STR_ACT
 
-*-INCLUDE ars3_READ_STATUT
TABLE FILE AXE_AARS16STATUT
PRINT VALEUR_AXE
WHERE NIVEAU EQ &NIV_STATUT.QUOTEDSTRING
-IF &NIV_STATUT EQ '1' THEN GOTO :STATUT_N1 ELSE IF &NIV_STATUT EQ '2' THEN GOTO :STATUT_N2 ELSE GOTO :STATUT_N3;
-:STATUT_N3
WHERE AARS16STATUT_N3 EQ '&EMPLOI'
-GOTO ESTATUT
-:STATUT_N2
WHERE AARS16STATUT_N2 EQ '&STATUT'
-GOTO ESTATUT
-:STATUT_N1
WHERE AARS16STATUT_N1 EQ '&STATUT_NATIONAL'
-ESTATUT
ON TABLE HOLD AS TMP_AXE_AARS16STATUT FORMAT ALPHA
END
-RUN
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_STAT='';
-REPEAT :BOUCLE_STATUT WHILE &IORETURN EQ 0;
-READ TMP_AXE_AARS16STATUT &STATUT_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE_STATUT;
-SET &STR_STAT=IF &STR_STAT EQ '' THEN &STATUT_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_STAT | '''' | ',' | '''' | &STATUT_VAL | '''' ELSE &STR_STAT | ',' | '''' | &STATUT_VAL | '''';
-SET &STR_STAT=IF &LINES EQ 1 THEN '''' | &STR_STAT | '''' ELSE &STR_STAT;
-SET &CPT=&CPT+1;
-:BOUCLE_STATUT
-CLOSE TMP_AXE_AARS16STATUT
-TYPE &STR_STAT
 
 
-*---------------------------------
-* Total ETP selon les paramètres
-*---------------------------------
SET CDN=OFF
-RUN
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE/D20.1
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
	WHERE ( AX02_CODE IN ( &STR_GEO ) )
-*Filtre STATUT
-*	WHERE (AX03_CODE IN ( &STR_STAT ))
-IF &STATUT NE 'STATUT' THEN GOTO STATNIV3;
WHERE (AX03_NIVEAU EQ 2)
-GOTO FINSTATNIV
-STATNIV3
WHERE (AX03_NIVEAU EQ 3)
-FINSTATNIV
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
 
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS OUT FORMAT ALPHA
END
 
-*Test pour vérifier qu'il y a des valeurs
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
-RUN
 
-READ OUT &VALEUR_FINALE.20.
 
-GOTO :VAL
 
-:ERRMESS
-SET &VALEUR_FINALE=0;
 
-:VAL
 
-SET &VAL_FI=TRIM('L', &VALEUR_FINALE, 20, ' ', 1,'A20');
-SET &NEWVAL1=TRUNCATE(&VAL_FI);
SET CDN=ON
-RUN
 
-SET &NEWVAL=FTOA(&NEWVAL1, '(D10.1)', 'A12');
-*Récupération inducteur
 
 
 
-*-INCLUDE ars3_RECUP_LIB_GEO_INDUCT
-IF '&NEW_REGION' EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN GOTO TSNREG;
 
TABLE FILE AXE_GEOAR
PRINT LIBELLE
WHERE VALEUR_AXE EQ '&NEW_REGION'
ON TABLE HOLD AS TMP_LIB_GEO_NREG FORMAT ALPHA
END
-RUN
 
-READ TMP_LIB_GEO_NREG &NEW_REGION_LIB.A60.
-GOTO ENREG
-TSNREG
-SET &NEW_REGION_LIB='Toutes';
 
-ENREG
-TYPE &NEW_REGION_LIB
 
-IF &REGION EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN GOTO TSREG;
 
TABLE FILE AXE_GEOAR
PRINT LIBELLE
WHERE VALEUR_AXE EQ '&REGION'
ON TABLE HOLD AS TMP_LIB_GEO_REG FORMAT ALPHA
END
-RUN
 
-READ TMP_LIB_GEO_REG &REGION_LIB.A60.
-GOTO EREG
-TSREG
-SET &REGION_LIB='Toutes';
 
-EREG
-TYPE &REGION_LIB
 
-SET &GEO_LIB = IF &GEO EQ 1 THEN 'National' ELSE IF &GEO EQ 2 THEN &NEW_REGION_LIB ELSE IF &GEO EQ 3 THEN &REGION_LIB;
 
 
 
-*-INCLUDE ars3_RECUP_LIB_ACTIVITE_INDUCT
-IF '&SECTEUR' EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN GOTO TSSECTEUR;
 
TABLE FILE AXE_AARS16INDUCTEUR
PRINT LIBELLE
WHERE VALEUR_AXE EQ '&SECTEUR'
ON TABLE HOLD AS TMP_LIB_ACTIVITE_SECTEUR FORMAT ALPHA
END
-RUN
 
-READ TMP_LIB_ACTIVITE_SECTEUR &SECTEUR_LIB.A60.
-GOTO ESECTEUR
-TSSECTEUR
-SET &SECTEUR_LIB='Tous';
-ESECTEUR
-TYPE &SECTEUR_LIB
 
 
 
 
 
-*-INCLUDE ars3_RECUP_LIB_STATUT
-*-IF '&STATUT' EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN GOTO TSSTATUT_LIB;
-*
-*TABLE FILE AXE_AARS16STATUT
-*PRINT LIBELLE
-*WHERE VALEUR_AXE EQ '&STATUT'
-*ON TABLE HOLD AS TMP_LIB_ACTIVITE_STATUT FORMAT ALPHA
-*END
-*-RUN
-*
-*
-*-READ TMP_LIB_ACTIVITE_STATUT &STATUT_LIB.A60.
-*-GOTO ESTATUT_LIB
-*-TSSTATUT_LIB
-*-SET &STATUT_LIB='Tous';
-*-ESTATUT_LIB
-*-TYPE &STATUT_LIB
 
 
-*-TYPE GEO_LIB = &GEO_LIB
-*-TYPE SECTEUR_LIB = &SECTEUR_LIB
-*-TYPE STATUT_LIB = &STATUT
-*-TYPE ETP = &NEWVAL
-*-EXIT
 
CREATE FILE STOCK_PARAM_PDF
-RUN
 
 
MODIFY FILE STOCK_PARAM_PDF
FREEFORM LIB_NOM_PARAM VAL_PARAM
MATCH  LIB_NOM_PARAM
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA
'ARS:','&GEO_LIB',$
'Secteur:','&SECTEUR_LIB',$
'Statut:','&STATUT',$
'ETP &EXERCICE :','&NEWVAL',$
END
-RUN
 
DEFINE FILE STOCK_PARAM_PDF
CPT/I2 WITH LIB_NOM_PARAM = CPT + 1 ;
END
 
 
TABLE FILE STOCK_PARAM_PDF
-*HEADING
-*"Paramètres utilisés pour produire le rapport"
PRINT LIB_NOM_PARAM AS '' VAL_PARAM AS '' CPT NOPRINT
-*FOOTING CENTER
-*"&VALEUR_FINALE<+0>ETP &EXERCICE"
-*"&VALEUR_FINALE ETP &EXERCICE"
ON TABLE SET PAGE-NUM NOLEAD
-*ON TABLE SET HTMLCSS ON
ON TABLE PCHOLD FORMAT PDF
ON TABLE SET STYLE *
-*INCLUDE=endeflt,$
-*TYPE=REPORT, GRID=OFF, BACKCOLOR=RGB(137 149 167),border-top-color=rgb(137 149 167), border-bottom-color=rgb(137 149 167),
-*border-left-color=rgb(137 149 167), border-right-color=rgb(137 149 167),SIZE=8,SQUEEZE=ON,$
TYPE=REPORT, SQUEEZE=ON,GRID=OFF,$
TYPE=DATA, SIZE=12, BACKCOLOR=( RGB(235 235 240) 'WHITE' ),$
TYPE=DATA, COLUMN=N1, FONT=ARIAL, COLOR=BLACK, STYLE=BOLD,$
TYPE=DATA, COLUMN=N2, FONT=ARIAL, COLOR=RGB(51 51 153),$
-*TYPE=FOOTING,JUSTIFY=CENTER,$
-*TYPE=FOOTING,LINE=1, OBJECT=TEXT, ITEM=1, SIZE=16, COLOR=RGB(255 102 0),$
-*TYPE=FOOTING,LINE=1, OBJECT=TEXT, ITEM=2, SIZE=16, COLOR=RGB(51 51 153),$
-*TYPE=FOOTING, HEADALIGN=BODY,$
-*TYPE=FOOTING,LINE=1, OBJECT=TEXT, ITEM=1, SIZE=12, COLOR=RGB(255 102 0),COLSPAN=1,JUSTIFY=RIGHT,$
-*TYPE=FOOTING,LINE=1, OBJECT=TEXT, ITEM=2, SIZE=12, COLOR=RGB(51 51 153),COLSPAN=1,$
-*RGB(234 234 255)
DEFMACRO=COND0001,MACTYPE=RULE,WHEN=N3 EQ 11,$
TYPE=DATA, COLUMN=N2, MACRO=COND0001, COLOR=RGB(255 102 0), SIZE=10, STYLE=BOLD,$
TYPE=DATA, COLUMN=N1, MACRO=COND0001, STYLE=BOLD,JUSTIFY=RIGHT,$
 
ENDSTYLE
END
