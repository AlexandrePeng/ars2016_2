-* File struct3_rpt_rep_ind.fex
SET CENT-ZERO = ON
SET CDN=SPACE
-*-SET &ECHO = ALL ;
-INCLUDE induct3_default
-INCLUDE induct3_read_statut
-INCLUDE induct3_read_geo
-DEFAULTH &ID_ELEMENT='D_GIDB001F'
 
-IF &NEW_REGION EQ '_FOC_NULL' THEN GOTO :PGARDE;
-IF &SECTEUR EQ '_FOC_NULL' THEN GOTO :PGARDE;
 
 
 
-************************ARS3_READ_ACTIVITE_INDUC pour le tableau des à coté du racapetp************************
-SET &NIV_ACTIV_SUP=&NIV_ACTIVITE+1;
TABLE FILE AXE_AARS17INDUCTEUR
PRINT VALEUR_AXE
WHERE NIVEAU EQ &NIV_ACTIV_SUP.QUOTEDSTRING
-IF &NIV_ACTIVITE EQ 1 THEN GOTO :ACTIVITE_N1;
WHERE AARS17IND_N2 EQ '&SECTEUR'
-:ACTIVITE_N1
ON TABLE HOLD AS TMP_AXE_AARS17INDUCTEUR FORMAT ALPHA
END
-RUN
 
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_ACT='';
-REPEAT :BOUCLE2 WHILE &IORETURN EQ 0;
-READ TMP_AXE_AARS17INDUCTEUR &ACT_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE2;
-SET &STR_ACT=IF &STR_ACT EQ '' THEN &ACT_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_ACT | '''' | ',' | '''' | &ACT_VAL | '''' ELSE &STR_ACT | ',' | '''' | &ACT_VAL | '''';
-SET &STR_ACT=IF &LINES EQ 1 THEN '''' | &STR_ACT | '''' ELSE &STR_ACT;
-SET &CPT=&CPT+1;
-:BOUCLE2
-CLOSE TMP_AXE_AARS17INDUCTEUR
 
TABLE FILE VIEW_IND_VDB_IA
SUM VALEUR_VDB/D10.1 AS 'Inducteurs'
BY AX04_CODE NOPRINT
BY AX04_LIBELLE AS 'Intitulés'
WHERE ID_ELEMENT EQ '&ID_ELEMENT'
-******Axe temps
WHERE AX01_CODE EQ '&EXERCICE'
WHERE AX01_NOM EQ 'TEMPS'
-******Axe GEO
WHERE AX02_NOM EQ 'GEOAR'
WHERE AX02_CODE IN (&STR_GEO)
-******Axe statut
 
-******Axe activité-inducteur
WHERE AX04_NOM EQ 'AARS17INDUCTEUR'
WHERE AX04_CODE IN (&STR_ACT)
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT HTML
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
     INCLUDE = endeflt,
$
TYPE=TITLE,
     COLUMN=N2,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     SIZE=10,
     COLOR='YELLOW',
     BACKCOLOR='PURPLE',
     STYLE=-UNDERLINE+BOLD,
$
TYPE=TITLE,
     COLUMN=N3,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     SIZE=10,
     COLOR='YELLOW',
     BACKCOLOR='PURPLE',
     STYLE=-UNDERLINE+BOLD,
$
ENDSTYLE
END
 
 
-GOTO :FINI
-:ERRMESS
-HTMLFORM BEGIN
<TD>Message d'information<BR>
<BR>
<I><B>Cette requête n'a pas retourné de données.<BR>
</I></B></TD>
-HTMLFORM END
-GOTO :FINI
 
-:PGARDE;
-*Page de garde lorsque aucune nouvelles région ou région n'est sélectionnée
-HTMLFORM INDUCT3_MIRE_ACCUEIL2
-:FINI
 
