-*============================================================================
-* Projet Suivi d'activité ARS 2016
-* Destinataire  : Philippe Louvel
-* Auteur        : Nicolas WATTIAUX (Information builders)
-* Date création : 04/09/2017
-* Description   : Procédure de génération des données pour la carte régionale
-*
-*----------------------------------------------------------------------------
-* Modifications
-*  Date   :
-*  Auteur :
 
-TYPE =============================================================================
-TYPE Procedure    : ars3_carto_depart.fex
-TYPE =============================================================================
-DEFAULTH &ETPNAT=0
-DEFAULTH &MINI=1742
-DEFAULTH &LEPAS=1
-DEFAULTH &STR_GEO='''_FOC_NULL'''
 
SET MSG=OFF
 
-INCLUDE ARS3_DEFAUT_ACT
 
 
-SET &VALREP=STRREP(&IBIMR_user.LENGTH,&IBIMR_user,1,'\',1,'_',&IBIMR_user.LENGTH,'A&IBIMR_user.LENGTH');
-*définition du libellé de la mesure utilisée (dans le tooltip pour la cartographie)
-SET &INDIC = '% ETP (Cf sortie Excel pour détail du calcul)' ;
-*Récupérer le nb de dept choisi
SET CDN=OFF
SET CENT-ZERO=ON
 
-TYPE Suppression de &LINES fichiers
-IF &DEPARTEMENT NE '_FOC_NULL' OR &NEW_REGION NE '_FOC_NULL' GOTO SK_AXE_DPT;
 
-SET &GEO=3;
TABLE FILE AXE_GEOAR
PRINT VALEUR_AXE
WHERE NIVEAU EQ &GEO
WHERE GEOAR_N2 EQ '&NEW_REGION'
ON TABLE HOLD AS TMP_AXE_GEO FORMAT ALPHA
END
-RUN
-GOTO FIN_AXE
-SK_AXE_DPT
-SET &GEO=4;
-*Récupération de la liste des nouvelles régions auxquelles appartiennent le(s) departement(s) sélectionné(s)
TABLE FILE AXE_GEOAR
PRINT GEOAR_N2
-IF &NEW_REGION EQ '_FOC_NULL' GOTO SKPNREG0;
WHERE GEOAR_N2 EQ '&NEW_REGION'
-SKPNREG0
-IF &DEPARTEMENT EQ '_FOC_NULL' GOTO SKPDPT0;
WHERE GEOAR_N4 EQ '&DEPARTEMENT'
-SKPDPT0
ON TABLE HOLD AS TMP_NEW_REG FORMAT ALPHA
END
-RUN
-*Récupération de la liste des départements de la nouvelle région associée
TABLE FILE AXE_GEOAR
PRINT VALEUR_AXE
WHERE NIVEAU EQ &GEO
-*WHERE GEOAR_N4 EQ &DEPARTEMENT
-*WHERE GEOAR_N3 EQ (TMP_REG)
WHERE GEOAR_N2 EQ (TMP_NEW_REG)
ON TABLE HOLD AS TMP_AXE_GEO FORMAT ALPHA
END
-RUN
 
-GOTO FIN_AXE
 
-FIN_AXE
 
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_GEO='';
-REPEAT :BOUCLE11 WHILE &IORETURN EQ 0;
-READ TMP_AXE_GEO &GEO_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE11;
-SET &STR_GEO=IF &STR_GEO EQ '' THEN &GEO_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_GEO | '''' | ',' | '''' | &GEO_VAL | '''' ELSE &STR_GEO | ',' | '''' | &GEO_VAL | '''';
-SET &STR_GEO=IF &LINES EQ 1 THEN '''' | &STR_GEO | '''' ELSE &STR_GEO;
-SET &CPT=&CPT+1;
-:BOUCLE11
-CLOSE TMP_AXE_GEO
 
-IF &DEPARTEMENT NE '_FOC_NULL' OR &NEW_REGION NE '_FOC_NULL' GOTO SECT_DEPT0;
 
-SECT_NREG0
TABLE FILE LISTE_NREG
PRINT LIB_NREG
WHERE CODE_GEOAR EQ &NEW_REGION.QUOTEDSTRING
-GOTO EDEP0
 
-SECT_DEPT0
TABLE FILE LISTE_DEPT
PRINT LIB_DEPT
WHERE CODE_GEOAR EQ (TMP_AXE_GEO)
-GOTO EDEP0
 
 
-EDEP0
ON TABLE SAVE
END
-RUN
 
 
 
-* Récupération du niveau d'activité sélectionné dans les filtres
-SET &NIVEAU_ACTI=IF (&SECTEUR NE '_FOC_NULL') AND (&TYPE_INT EQ '_FOC_NULL')  THEN 2 ELSE IF (&TYPE_INT NE '_FOC_NULL') AND (&MISSION EQ '_FOC_NULL') THEN 3 ELSE IF &MISSION NE '_FOC_NULL' THEN 4 ELSE 1;
-SET &NIV_ACTI_MOINS1= IF &NIVEAU_ACTI EQ 1 THEN 1 ELSE &NIVEAU_ACTI-1;
 
-* Récupération du niveau Geo sélectionné dans les filtres pour le dénominateur du calcul du ratio
-SET &NIVEAU_GEO=3;
-SET &NIVEAU_GEO_PLUS1=IF &NIVEAU_GEO EQ 4 THEN &NIVEAU_GEO ELSE &NIVEAU_GEO+1;
-* Calcul du Dénominateur du ratio\
 
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE/D20.1 AS 'ETPNAT'
-*Filtre INDICATEUR
-IF &NIVEAU_ACTI EQ 1 THEN GOTO :SKPBY;
BY AX02_CODE
-:SKPBY
WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
WHERE ( AX02_NIVEAU EQ &NIVEAU_GEO_PLUS1)
-*Filtre ENTITE
WHERE ( AX03_CODE IN ( 'AARS16ENT1ARS00' ) )
 
-*Filtre ACTIVITE
-IF &SECTEUR EQ '_FOC_NULL' GOTO WHTT_SECTEUR;
-IF &NIVEAU_ACTI GT 2 GOTO WHTT_SECTEUR;
-****** Vérification de l'axe corps(catégorie), si une catégorie est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui sélectionné dans le controle correspondant
-*******Vérification de l'axe corps(catégorie), si une catégorie est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui du niveau inférieur correspondant
-IF &STATUT NE '_FOC_NULL' THEN GOTO ACTI_NUMD;
WHERE ( AX04_CODE EQ 'AARS1600')
-GOTO ACTI_NUMEND
-ACTI_NUMD
WHERE ( AX04_CODE EQ '&SECTEUR')
-GOTO FINWHEREACT;
-ACTI_NUMEND
 
 
-WHTT_SECTEUR
-IF &TYPE_INT EQ '_FOC_NULL' GOTO WHTT_TYPE_INT;
-IF &NIVEAU_ACTI GT 3 GOTO WHTT_TYPE_INT;
-****** Vérification de l'axe corps(catégorie), si une catégorie est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui sélectionné dans le controle correspondant
-*******Vérification de l'axe corps(catégorie), si une catégorie est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui du niveau inférieur correspondant
-IF &STATUT NE '_FOC_NULL' THEN GOTO ACTI_NUMD1;
WHERE ( AX04_CODE EQ '&SECTEUR')
-GOTO ACTI_NUMEND1
-ACTI_NUMD1
WHERE ( AX04_CODE EQ '&TYPE_INT')
-GOTO FINWHEREACT;
-ACTI_NUMEND1
 
 
-WHTT_TYPE_INT
-IF &MISSION EQ '_FOC_NULL' GOTO WHTT_MISSION;
-IF &NIVEAU_ACTI GT 4 GOTO WHTT_MISSION;
-****** Vérification de l'axe corps(catégorie), si une catégorie est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui sélectionné dans le controle correspondant
-*******Vérification de l'axe corps(catégorie), si aucune catégorie n'est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui du niveau inférieur correspondant
-IF &STATUT NE '_FOC_NULL' THEN GOTO ACTI_NUMD2;
WHERE ( AX04_CODE EQ '&TYPE_INT')
-GOTO ACTI_NUMEND2
-ACTI_NUMD2
WHERE ( AX04_CODE EQ '&MISSION')
-GOTO FINWHEREACT;
-ACTI_NUMEND2
 
 
-WHTT_MISSION
WHERE ( AX04_NIVEAU EQ &NIV_ACTI_MOINS1)
 
-FINWHEREACT
 
-*Filtre PROGRAMME DE FINANCEMENT
WHERE ( AX05_CODE IN ( 'AARS15EFF0TOUS ' ) )
-*Filtre STATUT
-SET &STAT = IF &STATUT EQ '_FOC_NULL' THEN 'AARS16ST00TOUT' ELSE &STATUT;
WHERE ( AX06_CODE EQ '&STAT')
-*Filtre INSPECTION CONTROLE
WHERE ( AX07_CODE IN ( 'AARS15ICE_0TOUT ' ) )
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_NAT
END
-RUN
 
-*********SI on est au niveau 1 de l'axe ACTIVITE, alors on doit se positionner au niveau 1 de l'axe GEO, et récupérer la somme via une variable &ETPNAT, sinon
-*********on le conserve le résultat dans le tableau ETP_NAT
-IF &NIVEAU_ACTI NE 1 THEN GOTO SKIPREADNAT;
-READFILE ETP_NAT
-TYPE &ETPNAT
-*-EXIT
-GOTO RATIO
 
 
 
-SKIPREADNAT
-TYPE Niveau Activité sélectionnée
-*************** Calcul du numérateur du ratio******************************
 
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE/D20.1 AS 'ETPINTERMED'
AX02_LIBELLE
BY AX02_CODE
BY AX02_NIVEAU
-*Filtre INDICATEUR
WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
WHERE ( AX02_NIVEAU EQ &NIVEAU_GEO_PLUS1)
-*Filtre ENTITE
WHERE ( AX03_CODE IN ( 'AARS16ENT1ARS00' ) )
-*Filtre ACTIVITE
-IF &SECTEUR EQ '_FOC_NULL' GOTO WHTT_SECTEUR1;
-IF &NIVEAU_ACTI GT 2 GOTO WHTT_SECTEUR1;
-TYPE Niveau Géo=&NIVEAU_GEO_PLUS1
-TYPE SECTEUR=&SECTEUR
WHERE ( AX04_CODE EQ '&SECTEUR')
-WHTT_SECTEUR1
-IF &TYPE_INT EQ '_FOC_NULL' GOTO WHTT_TYPE_INT1;
-IF &NIVEAU_ACTI GT 3 GOTO WHTT_TYPE_INT1;
-TYPE Niveau Géo=&NIVEAU_GEO_PLUS1
-TYPE Affectation opérationnelle=&TYPE_INT
WHERE ( AX04_CODE EQ '&TYPE_INT')
-WHTT_TYPE_INT1
-IF &MISSION EQ '_FOC_NULL' GOTO WHTT_MISSION1;
-IF &NIVEAU_ACTI GT 4 GOTO WHTT_MISSION1;
-TYPE Niveau Géo=&NIVEAU_GEO_PLUS1
-TYPE Sous affectation operationnelle=&MISSION
WHERE ( AX04_CODE EQ '&MISSION')
-WHTT_MISSION1
-TYPE Niveau Géo=&NIVEAU_GEO_PLUS1
-TYPE Niveau Activité=&NIVEAU_ACTI
WHERE ( AX04_NIVEAU EQ &NIVEAU_ACTI)
-*Filtre PROGRAMME DE FINANCEMENT
WHERE ( AX05_CODE IN ( 'AARS15EFF0TOUS ' ) )
-*Filtre STATUT
-SET &STAT = IF &STATUT EQ '_FOC_NULL' THEN 'AARS16ST00TOUT' ELSE &STATUT;
WHERE ( AX06_CODE EQ '&STAT')
-*Filtre INSPECTION CONTROLE
WHERE ( AX07_CODE IN ( 'AARS15ICE_0TOUT ' ) )
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_INTERMED
END
-RUN
 
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
-***************************Calcul du ration ETP_INTERMED/ETP_NAT
 
-***************CAS 1: Le dénominateur est récupéré dans la variable &ETP_NAT
-* Lorsque le dénominateur fournit une seule ligne en sortie (SECTEUR=tous), calcul du ratio
-IF &NIVEAU_ACTI NE 1  THEN GOTO SKIPREADNAT2;
-RATIO
-TYPE CAS 1 Niveau Touts les Activités
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE/D20.1
AX02_LIBELLE
COMPUTE ETPNAT/D8.2=&ETPNAT;
COMPUTE CALC_DP/D20.1= (VALEUR_FINALE /&ETPNAT) *100;
COMPUTE CALC_DPT/D8=CALC_DP;
BY AX02_CODE
BY AX02_NIVEAU
-*Filtre INDICATEUR
WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
WHERE ( AX02_NIVEAU EQ &NIVEAU_GEO_PLUS1)
-*Filtre ENTITE
WHERE ( AX03_CODE IN ( 'AARS16ENT1ARS00' ) )
-*Filtre ACTIVITE et Filtre GEO
-IF &SECTEUR EQ '_FOC_NULL' GOTO WHTT_SECTEUR12;
-IF &NIVEAU_ACTI GT 2 GOTO WHTT_SECTEUR12;
WHERE ( AX04_CODE EQ '&SECTEUR')
 
-WHTT_SECTEUR12
-IF &TYPE_INT EQ '_FOC_NULL' GOTO WHTT_TYPE_INT12;
-IF &NIVEAU_ACTI GT 3 GOTO WHTT_TYPE_INT12;
WHERE ( AX04_CODE EQ '&TYPE_INT')
 
-WHTT_TYPE_INT12
-IF &MISSION EQ '_FOC_NULL' GOTO WHTT_MISSION12;
-IF &NIVEAU_ACTI GT 4 GOTO WHTT_MISSION12;
WHERE ( AX04_CODE EQ '&MISSION')
 
-WHTT_MISSION12
WHERE ( AX04_NIVEAU EQ &NIVEAU_ACTI)
 
-*Filtre PROGRAMME DE FINANCEMENT
WHERE ( AX05_CODE IN ( 'AARS15EFF0TOUS ' ) )
-*Filtre STATUT
-SET &STAT = IF &STATUT EQ '_FOC_NULL' THEN 'AARS16ST00TOUT' ELSE &STATUT;
WHERE ( AX06_CODE EQ '&STAT')
-*Filtre INSPECTION CONTROLE
WHERE ( AX07_CODE IN ( 'AARS15ICE_0TOUT ' ) )
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_RATIO
END
-RUN
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
 
 
-GOTO :TABLEFINALE;
 
-***************CAS 2: Le dénominateur est récupéré du tableau ETPNAT
-* Jointure entre les 2 tables numérateur et dénominateur pour effectuer les calcule en fonction des codes geo (axe 2)
-SKIPREADNAT2
-TYPE CAS 2 Niveau activité sélectionnée
-TYPE -- JOINTURE TABLE
JOIN CLEAR *
JOIN AX02_CODE IN ETP_INTERMED TO AX02_CODE IN ETP_NAT AS J1
END
-RUN
 
TABLE FILE ETP_INTERMED
SUM VALEUR_FINALE/D20.1 AS 'ETPINTERMED'
COMPUTE CALC_DP/D20.1=ETPINTERMED /ETPNAT *100;
COMPUTE CALC_DPT/D8=CALC_DP;
AX02_LIBELLE
BY AX02_CODE
BY AX02_NIVEAU
BY ETPNAT
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_RATIO
END
-RUN
 
 
-:TABLEFINALE;
 
-* Calcul de la moyenne, de la médiane, du maximum et du minimum à partir des tables de ratio *******
-TYPE --- CALCUL MOYENNE / MEDIANE / MINI / MAXI ---
TABLE FILE ETP_RATIO
SUM
AVE.CALC_DPT WITHIN AX02_NIVEAU AS 'MOYENNE'
MDN.CALC_DPT WITHIN AX02_NIVEAU AS 'MEDIANE'
MAX.CALC_DPT WITHIN AX02_NIVEAU AS 'MAXI'
MIN.CALC_DPT WITHIN AX02_NIVEAU AS 'MINI'
BY AX02_NIVEAU
BY AX02_CODE
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_CALC
END
-RUN
 
 
JOIN CLEAR *
JOIN AX02_CODE IN ETP_RATIO TO AX02_CODE IN ETP_CALC AS J1
TABLE FILE ETP_RATIO
SUM
COMPUTE CALC_MOY/D8.1=MOYENNE; NOPRINT
COMPUTE CALC_MEDIAN/D8.1=MEDIANE; NOPRINT
COMPUTE DI_CALC/D3.1=(CALC_MOY - CALC_MEDIAN) / CALC_MEDIAN; NOPRINT
COMPUTE DIFF_CALC/A3= FTOA(DI_CALC,'(D3.1)', 'A3');
MINI/I3
MAXI/I3
BY AX02_CODE NOPRINT
WHERE RECORDLIMIT EQ 1
ON TABLE SET CENT-ZERO ON
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS TBL_RESULT FORMAT ALPHA
END
-RUN
 
-IF &LINES EQ 0 GOTO CAS_NODATA;
 
-READ TBL_RESULT &DIFF_CALC.3. &MINI.3. &MAXI.3.
-CLOSE TBL_RESULT
-GOTO FCAS_NODATA
-CAS_NODATA
-SET &DIFF_CALC=100;
-SET &MINI=0;
-SET &MAXI=0;
-FCAS_NODATA
-TYPE &DIFF_CALC
-TYPE &MINI
-TYPE &MAXI
-SET &LEPAS=IF (&DIFF_CALC GT -0.05) AND (&DIFF_CALC LE 0.2) THEN (&MAXI - &MINI)/5 ELSE IF (&DIFF_CALC GT 0.2) THEN (&MAXI - &MINI)/(1+2+3+4+5) ELSE IF &DIFF_CALC LE -0.05 THEN (&MAXI - &MINI)/(1+2+3+4+5);
-SET &LEPAS= IF &LEPAS EQ 0 THEN 1 ELSE &LEPAS;
-SET &LEV0_DEB = &MINI ;
-TYPE LEPAS=&LEPAS
-SET &LEV0_FIN = IF (&DIFF_CALC GT -0.05) AND (&DIFF_CALC LE 0.2) THEN &LEV0_DEB + &LEPAS ELSE IF (&DIFF_CALC GT 0.2) THEN  &LEV0_DEB + 1 * &LEPAS ELSE IF &DIFF_CALC LE -0.05 THEN &LEV0_DEB + 5 * &LEPAS;
-TYPE 'LEV0=' &LEV0_FIN
-SET &LEV1_FIN = IF (&DIFF_CALC GT -0.05) AND (&DIFF_CALC LE 0.2) THEN &LEV0_FIN + &LEPAS  ELSE IF (&DIFF_CALC GT 0.2) THEN  &LEV0_FIN + 2 * &LEPAS ELSE IF &DIFF_CALC LE -0.05 THEN &LEV0_FIN + 4 * &LEPAS;
-SET &LEV2_FIN = IF (&DIFF_CALC GT -0.05) AND (&DIFF_CALC LE 0.2) THEN &LEV1_FIN + &LEPAS  ELSE IF (&DIFF_CALC GT 0.2) THEN  &LEV1_FIN + 3 * &LEPAS ELSE IF &DIFF_CALC LE -0.05 THEN &LEV1_FIN + 3 * &LEPAS;
-SET &LEV3_FIN = IF (&DIFF_CALC GT -0.05) AND (&DIFF_CALC LE 0.2) THEN &LEV2_FIN + &LEPAS  ELSE IF (&DIFF_CALC GT 0.2) THEN  &LEV2_FIN + 4 * &LEPAS ELSE IF &DIFF_CALC LE -0.05 THEN &LEV2_FIN + 2 * &LEPAS;
-*-SET &LEV4_FIN = IF (&DIFF_CALC GT -0.05) AND (&DIFF_CALC LE 0.2) THEN &LEV3_FIN + &LEPAS  ELSE IF (&DIFF_CALC GT 0.2) THEN  &LEV3_FIN + 5 * &LEPAS ELSE IF &DIFF_CALC LE -0.05 THEN &LEV3_FIN + 1 * &LEPAS;
 
-SET &NB_DEPT = &LINES ;
-IF &DEPARTEMENT NE '_FOC_NULL' GOTO SECT_DEPT1;
-IF &DEPARTEMENT EQ '_FOC_NULL' AND &NEW_REGION EQ '_FOC_NULL' GOTO SECT_ALL;
-IF &NEW_REGION NE '_FOC_NULL' GOTO SECT_DEPT1;
-IF &NEW_REGION EQ '_FOC_NULL' GOTO SECT_NREG1;
 
-SECT_ALL
TABLE FILE LISTE_DEPT
BY CODE_GEOAR
BY CODE_DEPT
-GOTO EDEP1
-SECT_DEPT1
TABLE FILE LISTE_DEPT
BY CODE_GEOAR
BY CODE_DEPT
WHERE CODE_GEOAR EQ (TMP_AXE_GEO)
-GOTO EDEP1
 
 
-* permet la non apparition de la carte régionale lorsqu'aucune valeur de l'axe géo n'est sélectionnée
-SECT_NREG1
TABLE FILE LISTE_NREG
BY CODE_GEOAR
BY CODE_NREG
WHERE (CODE_GEOAR IN (&STR_GEO) )
 
-EDEP1
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS SCORE FORMAT ALPHA
END
-RUN
 
 
JOIN CLEAR *
JOIN CODE_GEOAR IN SCORE TO AX02_CODE IN ETP_RATIO AS J1
 
-IF &DEPARTEMENT NE '_FOC_NULL' GOTO JOIN_DEPT;
 
JOIN CODE_DEPT IN SCORE TO DEPT IN DEPT AS J2
-GOTO FIN_JOIN
-JOIN_DEPT
JOIN CODE_DEPT IN SCORE TO DEPT IN DEPT AS J2
-FIN_JOIN
 
-*Génération de ma syntaxe
APP FI PREPJSOND DISK prepjsonr.js (APPEND
-RUN
 
-WRITE PREPJSOND var statesData = {
-WRITE PREPJSOND "type": "FeatureCollection","features": [
-RUN
SET CDN=ON
-*Récup des syntaxes
DEFINE FILE SCORE
SCORE/A35 = '"density": "' || FTOA(CALC_DPT,'(D8)', 'A10') || '",' ;
END
 
TABLE FILE SCORE
PRINT DEBUT
SCORE
FIN
 
-IF &DEPARTEMENT NE '_FOC_NULL' GOTO SECT_DEPT3;
 
 
BY DEPT NOPRINT
-GOTO EDEP4
 
-SECT_DEPT3
BY DEPT NOPRINT
 
-EDEP4
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE SAVE AS PREPAD FORMAT ALPHA
END
-RUN
 
-*Relecture du fichier avec un master générique pour faire sauter la fin du TX
EX -LINES *  EDAPUT MASTER, LIGNE135, C, MEM,
FILE=LIGNE135, SUFFIX=FIX,$
SEGNAME=LIGNE135, $
GROUP=FULLRECORD, ALIAS=FULLRECORD, USAGE=A135, ACTUAL=A135,$
FIELD=2CHAR,A2, A2, $
FIELD=RESTE,A133,A133,$
EDAPUT*
-RUN
 
-*Traitement, remplacement de %$ par , sauf pour le dernier qu'il faut supprimer
APP FI LIGNE135 DISK prepad.ftm ( LRECL 135
-RUN
 
DEFINE FILE LIGNE135
COMPTEUR/I5  = LAST COMPTEUR + 1 ;
END
 
TABLE FILE LIGNE135
SUM MAX.COMPTEUR NOPRINT
PRINT COMPTEUR NOPRINT
COMPUTE LIGNEOK/A135 = IF 2CHAR NE '%$' THEN FULLRECORD ELSE ',' ;
WHERE TOTAL MAX.COMPTEUR NE COMPTEUR  ;
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE SAVE AS PREPJSOND FORMAT ALPHA
END
-RUN
 
 
-WRITE PREPJSOND ]
-WRITE PREPJSOND }
-RUN
 
-*Récupération du chemin où copier le js généré par la carto
-SET &NOMOUT = 'ars3_' || 'regjson_' || &DMYY || EDIT(&TOD,'99$99$99') || '_' || &VALREP || '.js' ;
-SET &CHEMIN=&APPROOT | '\carto\' || &NOMOUT;
 
-* Copie du du js dans le répertoire carto
!copy prepjsonr.js &CHEMIN
-*J'ai mon fichier propre
-*appel de la page HTML De display
-*-EXIT
-IF &EXCEL_LAUNCH='OUI' GOTO SKP_CARTO;
-HTMLFORM map_regional
-:ERRMESS
-INCLUDE ARS3_NODATA
-SKP_CARTO
