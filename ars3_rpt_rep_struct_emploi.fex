-* File ars3_rpt_rep_struct.fex
-*============================================================================
-* Projet Suivi d'activité ARS 2014 - Tableau de bord contrôleur de gestion
-*
-* Destinataire  : Philippe Louvel
-* Auteur        : Nicolas Wattiaux (Information Builders France) / Guy Futé (Information Builders France)
-* Date création : 27/01/2016
-* Description   : Enquête Activité ARS par Statut
-*
-*----------------------------------------------------------------------------
-* Modifications
-*  Date   : jj/mm/ssaa
-*  Auteur : first last - sté
-*  Objet  : ...
-*============================================================================
-*-*Ajout du 0 avant les décimales lorque l'on a une valeur compris entre 0 et 1
 
-SET &ECHO = ALL ;
-DEFAULTH &GEOAR_N2='_FOC_NULL'
-DEFAULTH &AX02_LIBELLE=''
-*SET NODATA=0
SET CENT-ZERO = ON
-TYPE ============================================================================
-TYPE Procédure    : ars3_rpt_rep_struct_emploi.fex
-TYPE ============================================================================
 
-*------------------------------------------------------------------
-* Definition du passage de paramètre pour Total ETP et % du total
-*------------------------------------------------------------------
 
-INCLUDE ars3_DEFAUT_STAT
-*Renvoie à la page de gar lorsqu'aucun contrôle de l'axe géo n'est sélectionné
-IF &NEW_REGION EQ '_FOC_NULL' AND &REGION EQ '_FOC_NULL' THEN GOTO :PGARDE;
-SET &GEO=2;
-IF &NEW_REGION EQ '_FOC_NULL' AND &REGION NE '_FOC_NULL' THEN GOTO :CALCGEO;
 
-INCLUDE ars3_READ_GEO
-GOTO :FINCALCGEO;
 
-:CALCGEO
-*Récupérer le code de la nouvelle région lorsqu'il n'y a aucune sélection sur le "contrôle Nouvelle région" et une sélection sur le contrôle "région"
TABLE FILE AXE_GEOAR
SUM GEOAR_N2
WHERE GEOAR_N3 EQ &REGION.QUOTEDSTRING
ON TABLE HOLD AS TMP_AXE_GEO FORMAT ALPHA
END
-RUN
 
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_GEO='';
-REPEAT :BOUCLE1 WHILE &IORETURN EQ 0;
-READ TMP_AXE_GEO &GEO_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE1;
-SET &STR_GEO=IF &STR_GEO EQ '' THEN &GEO_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_GEO | '''' | ',' | '''' | &GEO_VAL | '''' ELSE &STR_GEO | ',' | '''' | &GEO_VAL | '''';
-SET &STR_GEO=IF &LINES EQ 1 THEN '''' | &STR_GEO | '''' ELSE &STR_GEO;
-SET &CPT=&CPT+1;
-:BOUCLE1
-CLOSE TMP_AXE_GEO
-TYPE &STR_GEO
-:FINCALCGEO
 
-********************************************************
-SET &NIV_ACTIVITE=IF &MISSION NE '_FOC_NULL' THEN '4' ELSE IF &TYPE_INT NE '_FOC_NULL' THEN '3' ELSE IF &SECTEUR NE '_FOC_NULL' THEN '2' ELSE '1';
-INCLUDE ars3_READ_ACTIVITE
-INCLUDE ars3_READ_STATUT_SPECIAL
 
-* A continuer
-*la colonne 1 représentant le pourcentage de la nouvelle région :
-*----------------------------------------------------
-*Calcul des numérateurs (ETP pour les statuts sélectionnés)
DEFINE FILE VIEW_IND_VAL_IA
VALEUR_FIN/D20.2= IF VALEUR_FINALE IS MISSING THEN 0 ELSE VALEUR_FINALE ;
END
TABLE FILE VIEW_IND_VAL_IA
SUM
     VALEUR_FIN AS 'VALEUR_NREG'
     PCT.VALEUR_FIN AS 'PCT_NREG'
BY AX02_ORDRE
BY AX02_CODE
BY AX02_LIBELLE AS 'NREGION'
BY AX06_ORDRE
BY AX06_CODE
BY AX06_LIBELLE
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
	WHERE ( AX02_CODE IN ( &STR_GEO ) )
-*	WHERE ( AX02_CODE EQ &NEW_REGION.QUOTEDSTRING )
WHERE ( AX02_NIVEAU EQ 2)
-*Filtre ENTITE
	WHERE ( AX03_NIVEAU EQ 1 )
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
-*Filtre PLAFOND
	WHERE ( AX05_NIVEAU EQ 1 )
-*Filtre STATUT
	WHERE ( AX06_CODE IN ( &STR_STAT ) )
-*Filtre INSPECTION CONTROLE (sinon la valeur restituée est doublée)
	WHERE  AX07_NIVEAU EQ 1
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_FNEWREG FORMAT ALPHA
 
END
-RUN
 
DEFINE FILE TMP_FNEWREG
ORDRE/I1=1;
LIBNREGION/A70='Région ' | NREGION ;
END
TABLE FILE TMP_FNEWREG
SUM
VALEUR_NREG AS 'VALO'
AX06_LIBELLE
 
BY AX06_CODE
BY LIBNREGION AS 'AXEGEO'
BY ORDRE
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TAB_NREG FORMAT FOCUS INDEX AX06_CODE
END
 
TABLE FILE VIEW_IND_VAL_IA
SUM
     VALEUR_FIN AS 'VALEUR_REG'
     PCT.VALEUR_FIN AS 'PCT_REGION'
BY AX02_ORDRE
BY AX02_CODE
BY AX02_LIBELLE AS 'REGION'
BY AX06_ORDRE
BY AX06_CODE
BY AX06_LIBELLE
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
	WHERE ( AX02_CODE_SUP IN ( &STR_GEO ) )
WHERE ( AX02_NIVEAU EQ 3)
-*Filtre ENTITE
	WHERE ( AX03_NIVEAU EQ 1 )
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
-*Filtre PLAFOND
	WHERE ( AX05_NIVEAU EQ 1 )
-*Filtre STATUT
	WHERE ( AX06_CODE IN ( &STR_STAT ) )
-*Filtre INSPECTION CONTROLE (sinon la valeur restituée est doublée)
	WHERE  AX07_NIVEAU EQ 1
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_FREG FORMAT ALPHA
 
END
 
-RUN
DEFINE FILE TMP_FREG
ORDRE/I1=2;
LIBREGION/A70='ARS ' | REGION;
END
TABLE FILE TMP_FREG
SUM
VALEUR_REG AS 'VALO'
AX06_LIBELLE
BY AX06_CODE
BY LIBREGION AS 'AXEGEO'
BY ORDRE
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TAB_REG FORMAT FOCUS INDEX AX06_CODE
END
 
 
TABLE FILE VIEW_IND_VAL_IA
SUM
     VALEUR_FIN AS 'VALEUR_NATIO'
     PCT.VALEUR_FIN AS 'PCT_NATIO'
BY AX02_ORDRE
BY AX02_CODE
BY AX02_LIBELLE AS 'NATIONAL'
BY AX06_ORDRE
BY AX06_CODE
BY AX06_LIBELLE
 
 
 
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
WHERE ( AX02_NIVEAU EQ 1)
-*Filtre ENTITE
	WHERE ( AX03_NIVEAU EQ 1 )
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
-*Filtre PLAFOND
	WHERE ( AX05_NIVEAU EQ 1 )
-*Filtre STATUT
	WHERE ( AX06_CODE IN ( &STR_STAT ) )
-*Filtre INSPECTION CONTROLE (sinon la valeur restituée est doublée)
	WHERE  AX07_NIVEAU EQ 1
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_FNAT FORMAT ALPHA
END
-RUN
DEFINE FILE TMP_FNAT
ORDRE/I1=3;
LIBNATIO/A70='National';
END
TABLE FILE TMP_FNAT
SUM
VALEUR_NATIO AS 'VALO'
AX06_LIBELLE
 
BY AX06_CODE
BY LIBNATIO AS 'AXEGEO'
BY ORDRE
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TAB_NATIO FORMAT FOCUS INDEX AX06_CODE
END
 
-* Regroupement des trois tables sur les différents niveaux géographiques (Nouvelle région, Région, National)
USE
TAB_NREG AS TAB_NREG
TAB_REG AS TAB_NREG
TAB_NATIO AS TAB_NREG
END
 
 
-SET &SCALE=150;
SET HOLDLIST=PRINTONLY
 
TABLE FILE TAB_NREG
SUM PCT.VALO
ON TABLE SAVE
END
-RUN
-READ SAVE,&PVALO
-RUN
 
 
-DEFAULT &HLIBAXE=' ',&LIBR1='',&LIBR2='',&LIBR3='',&LIBR4=''
TABLE FILE TAB_NREG
SUM AXEGEO AS 'HLIBAXE'
BY ORDRE NOPRINT
BY AXEGEO NOPRINT
ON TABLE SET ASNAME ON
ON TABLE HOLD AS HREG FORMAT ALPHA
END
-RUN
 
-SET &ANCREG = &LINES - 2;
-SET &NBVAL = &LINES;
-SET &VALTOT = 'VAL' || EDIT(&NBVAL.EVAL) || '/D20.2';
-REPEAT :LECTREG FOR &I FROM 1 TO &NBVAL
-READFILE HREG
-SET &LIBR.&I = &HLIBAXE;
-:LECTREG
 
JOIN BLANK WITH VALO IN TAB_NREG TO FBLANK IN MCGYVER
 
DEFINE FILE TAB_NREG
BLANK/A1 WITH VALO = ' ';
VAL1/D20.2 = IF CPT EQ 1 AND ORDRE EQ 1 THEN VALO ELSE 0;
-REPEAT :GENVAL FOR &I FROM 1 TO &ANCREG
-SET &J= &I + 1;
-SET &L = '&' || 'LIBR' || EDIT(&J);
-SET &VAL = 'VAL' || EDIT(&J) || '/D20.2';
&VAL = IF CPT EQ &J AND ORDRE EQ 2 AND AXEGEO EQ '&L.EVAL' THEN VALO ELSE 0;
-:GENVAL
&VALTOT = IF CPT EQ &NBVAL AND ORDRE EQ 3 THEN VALO ELSE 0;
END
 
TABLE FILE TAB_NREG
SUM
-REPEAT :GENPRT FOR &I FROM 1 TO &NBVAL
-SET &L = '&' || 'LIBR' || EDIT(&I);
    PCT.VAL&I/D9.2% AS '&L.EVAL'
	COMPUTE PCOST/I4=PCT.VAL&I / &PVALO * &SCALE; NOPRINT
	COMPUTE ARCOST/A4=EDIT(PCOST); NOPRINT
	COMPUTE BAR/A100=IF &I EQ &NBVAL THEN '<IMG SRC="/ibi_html/vis/gff00ff.gif" WIDTH=' | ARCOST | ' HEIGHT=18>' ELSE IF &I EQ 1 THEN '<IMG SRC="/ibi_html/vis/g008000.gif" WIDTH=' | ARCOST | ' HEIGHT=18>' ELSE '<IMG SRC="/ibi_html/vis/g000080.gif" WIDTH=' | ARCOST | ' HEIGHT=18>'; AS ''
 
-:GENPRT
BY HIGHEST TOTAL PCT.VAL1 NOPRINT
BY AX06_LIBELLE AS '          '
IF CPT LE &NBVAL
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE COLUMN-TOTAL AS 'TOTAL'
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
     INCLUDE = endeflt,
$
TYPE=REPORT,SQUEEZE=ON,GRID=ON,SIZE=10,BORDER-TOP=LIGHT,BORDER-BOTTOM=LIGHT,BORDER-LEFT=LIGHT,BORDER-RIGHT=LIGHT,$
TYPE=GRANDTOTAL, BACKCOLOR=RGB(184 204 228),STYLE=BOLD,JUSTIFY=CENTER,$
TYPE=GRANDTOTAL,COLUMN=N3,BORDER-RIGHT=OFF,$
TYPE=GRANDTOTAL,COLUMN=N7,BORDER-RIGHT=OFF,$
TYPE=GRANDTOTAL,COLUMN=N11,BORDER-RIGHT=OFF,$
TYPE=GRANDTOTAL,COLUMN=N15,BORDER-RIGHT=OFF,$
TYPE=GRANDTOTAL,COLUMN=N19,BORDER-RIGHT=OFF,$
TYPE=TITLE,BACKCOLOR=RGB(184 204 228),STYLE=BOLD,JUSTIFY=CENTER,$
TYPE=TITLE,COLUMN=N2, BACKCOLOR=RGB(255 255 255),BORDER-TOP=OFF,BORDER-LEFT=OFF,$
TYPE=REPORT,COLUMN=N2,SQUEEZE=3,$
TYPE=DATA,COLUMN=N3,JUSTIFY=CENTER,BORDER-RIGHT=OFF,$
TYPE=TITLE,COLUMN=N3,BORDER-RIGHT=OFF,$
TYPE=DATA,COLUMN=N7,JUSTIFY=CENTER,BORDER-RIGHT=OFF,$
TYPE=TITLE,COLUMN=N7,BORDER-RIGHT=OFF,$
TYPE=DATA,COLUMN=N11,JUSTIFY=CENTER,BORDER-RIGHT=OFF,$
TYPE=TITLE,COLUMN=N11,BORDER-RIGHT=OFF,$
TYPE=DATA,COLUMN=N15,JUSTIFY=CENTER,BORDER-RIGHT=OFF,$
TYPE=TITLE,COLUMN=N15,BORDER-RIGHT=OFF,$
TYPE=DATA,COLUMN=N19,JUSTIFY=CENTER,BORDER-RIGHT=OFF,$
TYPE=TITLE,COLUMN=N19,BORDER-RIGHT=OFF,$
ENDSTYLE
END
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
 
-GOTO :FINI
-:ERRMESS
-HTMLFORM BEGIN
<TD>Message d'information<BR>
<BR>
<I><B>Cette requête n'a pas retourné de données.<BR>
</I></B></TD>
-HTMLFORM END
-GOTO :FINI
 
-:PGARDE;
-*Page de garde lorsque aucune nouvelles région ou région n'est sélectionnée
-HTMLFORM ars3_SE_MIRE_ACCUEIL
-:FINI
 
 
 
