-* File ars3_rpt_rep_struct.fex
-*============================================================================
-* Projet Suivi d'activité ARS 2014 - Tableau de bord contrôleur de gestion
-*
-* Destinataire  : Philippe Louvel
-* Auteur        : Nicolas Wattiaux (Information Builders France) / Guy Futé (Information Builders France)
-* Date création : 27/01/2016
-* Description   : Enquête Activité ARS par Statut
-*
-*----------------------------------------------------------------------------
-* Modifications
-*  Date   : jj/mm/ssaa
-*  Auteur : first last - sté
-*  Objet  : ...
-*============================================================================
-*-*Ajout du 0 avant les décimales lorque l'on a une valeur compris entre 0 et 1
 
-SET &ECHO = ALL ;
-DEFAULTH &GEOAR_N2='_FOC_NULL'
-DEFAULTH &AX02_LIBELLE=''
-*SET NODATA=0
SET CENT-ZERO = ON
-TYPE ============================================================================
-TYPE Procédure    : ars3_rpt_rep_struct_emploi.fex
-TYPE ============================================================================
 
-*------------------------------------------------------------------
-* Definition du passage de paramètre pour Total ETP et % du total
-*------------------------------------------------------------------
 
-INCLUDE ars3_DEFAUT_STAT
-IF &NEW_REGION EQ '_FOC_NULL' AND &REGION EQ '_FOC_NULL' THEN GOTO :PGARDE;
 
-SET &GEO=2;
 
-*-IF &NEW_REGION NE '_FOC_NULL' THEN GOTO :SUITEGEO;
-*
-*TABLE FILE AXE_GEOAR
-*SUM GEOAR_N2
-*WHERE GEOAR_N3 EQ &REGION.QUOTEDSTRING
-*ON TABLE HOLD AS TMP_AXE_GEO_2 FORMAT ALPHA
-*END
-*-RUN
-*
-*-READFILE TMP_AXE_GEO_2
-*
-*
-*-SET &NEW_REGION0=TRUNCATE(&GEOAR_N2);
-*-SET &NEW_REGION=&NEW_REGION0;
-*
-*-:SUITEGEO;
-INCLUDE ars3_READ_GEO
-SET &NIV_ACTIVITE=IF &MISSION NE '_FOC_NULL' THEN '4' ELSE IF &TYPE_INT NE '_FOC_NULL' THEN '3' ELSE IF &SECTEUR NE '_FOC_NULL' THEN '2' ELSE '1';
-INCLUDE ars3_READ_ACTIVITE
-INCLUDE ars3_READ_STATUT_SPECIAL
 
-* A continuer
-*la colonne 1 représentant le pourcentage de la nouvelle région :
-*----------------------------------------------------
-*Calcul des numérateurs (ETP pour les statuts sélectionnés)
DEFINE FILE VIEW_IND_VAL_IA
VALEUR_FIN/D20.2= IF VALEUR_FINALE IS MISSING THEN 0 ELSE VALEUR_FINALE ;
END
TABLE FILE VIEW_IND_VAL_IA
SUM
     VALEUR_FIN AS 'VALEUR_NREG'
     PCT.VALEUR_FIN AS 'PCT_NREG'
BY AX02_ORDRE
BY AX02_CODE
BY AX02_LIBELLE AS 'NREGION'
BY AX06_ORDRE
BY AX06_CODE
BY AX06_LIBELLE
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
	WHERE ( AX02_CODE IN ( &STR_GEO ) )
-*	WHERE ( AX02_CODE EQ &NEW_REGION.QUOTEDSTRING )
WHERE ( AX02_NIVEAU EQ 2)
-*Filtre ENTITE
	WHERE ( AX03_NIVEAU EQ 1 )
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
-*Filtre PLAFOND
	WHERE ( AX05_NIVEAU EQ 1 )
-*Filtre STATUT
	WHERE ( AX06_CODE IN ( &STR_STAT ) )
-*Filtre INSPECTION CONTROLE (sinon la valeur restituée est doublée)
	WHERE  AX07_NIVEAU EQ 1
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_FNEWREG FORMAT ALPHA
 
END
-RUN
 
DEFINE FILE TMP_FNEWREG
ORDRE/I1=1;
LIBNREGION/A70='Région ' | NREGION ;
END
TABLE FILE TMP_FNEWREG
SUM
VALEUR_NREG AS 'VALO'
AX06_LIBELLE
 
BY AX06_CODE
BY LIBNREGION AS 'AXEGEO'
BY ORDRE
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TAB_NREG FORMAT FOCUS INDEX AX06_CODE
END
 
TABLE FILE VIEW_IND_VAL_IA
SUM
     VALEUR_FIN AS 'VALEUR_REG'
     PCT.VALEUR_FIN AS 'PCT_REGION'
BY AX02_ORDRE
BY AX02_CODE
BY AX02_LIBELLE AS 'REGION'
BY AX06_ORDRE
BY AX06_CODE
BY AX06_LIBELLE
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
	WHERE ( AX02_CODE_SUP IN ( &STR_GEO ) )
WHERE ( AX02_NIVEAU EQ 3)
-*Filtre ENTITE
	WHERE ( AX03_NIVEAU EQ 1 )
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
-*Filtre PLAFOND
	WHERE ( AX05_NIVEAU EQ 1 )
-*Filtre STATUT
	WHERE ( AX06_CODE IN ( &STR_STAT ) )
-*Filtre INSPECTION CONTROLE (sinon la valeur restituée est doublée)
	WHERE  AX07_NIVEAU EQ 1
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_FREG FORMAT ALPHA
 
END
 
-RUN
DEFINE FILE TMP_FREG
ORDRE/I1=2;
LIBREGION/A70='ARS ' | REGION;
END
TABLE FILE TMP_FREG
SUM
VALEUR_REG AS 'VALO'
AX06_LIBELLE
BY AX06_CODE
BY LIBREGION AS 'AXEGEO'
BY ORDRE
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TAB_REG FORMAT FOCUS INDEX AX06_CODE
END
 
 
TABLE FILE VIEW_IND_VAL_IA
SUM
     VALEUR_FIN AS 'VALEUR_NATIO'
     PCT.VALEUR_FIN AS 'PCT_NATIO'
BY AX02_ORDRE
BY AX02_CODE
BY AX02_LIBELLE AS 'NATIONAL'
BY AX06_ORDRE
BY AX06_CODE
BY AX06_LIBELLE
 
 
 
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
WHERE ( AX02_NIVEAU EQ 1)
-*Filtre ENTITE
	WHERE ( AX03_NIVEAU EQ 1 )
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
-*Filtre PLAFOND
	WHERE ( AX05_NIVEAU EQ 1 )
-*Filtre STATUT
	WHERE ( AX06_CODE IN ( &STR_STAT ) )
-*Filtre INSPECTION CONTROLE (sinon la valeur restituée est doublée)
	WHERE  AX07_NIVEAU EQ 1
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_FNAT FORMAT ALPHA
END
-RUN
DEFINE FILE TMP_FNAT
ORDRE/I1=3;
LIBNATIO/A70='National';
END
TABLE FILE TMP_FNAT
SUM
VALEUR_NATIO AS 'VALO'
AX06_LIBELLE
 
BY AX06_CODE
BY LIBNATIO AS 'AXEGEO'
BY ORDRE
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TAB_NATIO FORMAT FOCUS INDEX AX06_CODE
END
 
USE
TAB_NREG AS TAB_NREG
TAB_REG AS TAB_NREG
TAB_NATIO AS TAB_NREG
END
 
-SET &SCALE=200;
SET HOLDLIST=PRINTONLY
 
TABLE FILE TAB_NREG
SUM PCT.VALO
ON TABLE SAVE
END
-RUN
-READ SAVE,&PVALO
-RUN
 
DEFINE FILE TAB_NREG
LIBELLE/A80=AXEGEO;
VALEUR/D6.2%=VALO;
END
TABLE FILE TAB_NREG
SUM PCT.VALEUR AS ''
COMPUTE PCOST/I4=PCT.VALEUR / &PVALO * &SCALE; NOPRINT
COMPUTE ARCOST/A4=EDIT(PCOST); NOPRINT
COMPUTE BAR/A100=IF ORDRE EQ 3 THEN '<IMG SRC="/ibi_html/vis/gff00ff.gif" WIDTH=' | ARCOST | ' HEIGHT=18>' ELSE IF ORDRE EQ 2 THEN '<IMG SRC="/ibi_html/vis/g000080.gif" WIDTH=' | ARCOST | ' HEIGHT=18>' ELSE '<IMG SRC="/ibi_html/vis/g008000.gif" WIDTH=' | ARCOST | ' HEIGHT=18>'; AS ''
BY TOTAL HIGHEST VALEUR NOPRINT
BY AX06_LIBELLE AS ''
ACROSS ORDRE NOPRINT
ACROSS LIBELLE AS ''
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE COLUMN-TOTAL AS 'TOTAL'
ON TABLE PCHOLD FORMAT HTML
-*ON TABLE SET CACHELINES 20
-*ON TABLE SET AUTOFIT ON
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
     INCLUDE = endeflt,
$
-*GRAPHTYPE=DATA,ACROSSCOLUMN=N1,GRAPHCOLOR='BLUE',GRAPHLENGTH=1,$
TYPE=ACROSSVALUE,SIZE=11,BACKCOLOR=RGB(184 204 228),STYLE=BOLD,JUSTIFY=CENTER,$
TYPE=REPORT, SQUEEZE = ON, HEADALIGN=BODY,GRID=ON,BORDER-TOP=LIGHT,BORDER-LEFT=LIGHT,BORDER-RIGHT=LIGHT,BORDER-BOTTOM=LIGHT, SIZE= 11,VERTICAL-ALIGN=CENTER,$
TYPE=GRANDTOTAL, BACKCOLOR=RGB(184 204 228),STYLE=BOLD,$
ENDSTYLE
END
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
 
-GOTO :FINI
-:ERRMESS
-HTMLFORM BEGIN
<TD>Message d'information<BR>
<BR>
<I><B>Cette requête n'a pas retourné de données.<BR>
</I></B></TD>
-HTMLFORM END
-GOTO :FINI
 
-:PGARDE;
-*Page de garde lorsque aucune nouvelles région ou région n'est sélectionnée
-HTMLFORM ars3_INDUCT_MIRE_ACCUEIL
-:FINI
 
 
