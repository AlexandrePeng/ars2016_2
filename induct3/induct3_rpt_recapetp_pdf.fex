-* File dir2_rpt_recapetp_induct.fex
-*==================================================================================================
-* Projet Inducteur de charges 2016: Récapitulatif des paramètres, Total ETP et inducteur
-*
-* Destinataire  : Philippe Louvel
-* Auteur        : Alexandre PENG
-* Date création : 14/11/2017
-* Description   :
-*
-*--------------------------------------------------------------------------------------------------
-* Modifications
-*  Date   : jj/mm/ssaa
-*  Auteur : first last - sté
-*  Objet  : ...
-*==================================================================================================
 
 
 
-*-SET &ECHO = ALL
SET SPACES =ON;
 
-DEFAULTH &AX04_LIBELLE = ''
-DEFAULTH &VALEUR_FINALE = 0
-*-INCLUDE ARS3_2016_DEFAUT_INDUCT_PDF
 
-DEFAULTH &ID_INDICATEUR='I_GIDB001E';
-DEFAULTH &ID_INDUCTEUR='D_GIDB001F';
-DEFAULTH &EXERCICE='2016';
 
-DEFAULTH &NOM_VUE = 'VUE_ACTIVITE'
 
-*Filtre GEO
-DEFAULTH &GEO='1';
-DEFAULTH &GEO_NATIONAL='GEOA000';
-DEFAULTH &NEW_REGION='_FOC_NULL';
 
 
-*Filtre STATUT
-DEFAULTH &STATUT_NIV='1';
-DEFAULTH &STATUT_NATIONAL='AARS17ST00TOUT';
-DEFAULTH &STATUT='_FOC_NULL';
-DEFAULTH &EMPLOI='_FOC_NULL';
 
-*Filtre ACTIVITE
-DEFAULTH &ACTIVITE='1';
-DEFAULTH &ACTIVITE_NATIONAL='AARS17INDB00';
-DEFAULTH &SECTEUR='_FOC_NULL';
-DEFAULTH &INDUCTEUR='_FOC_NULL';
 
 
-*******Récupération des valeur de niveau pour chaque filtre
-DEFAULTH &STR_GEO='';
-DEFAULTH &STR_ACT='';
 
SET CENT-ZERO=ON
SET CDN=ON
 
-SET &GEO = IF &NEW_REGION EQ 'undefined' OR 'TOUT' OR '_FOC_NULL' THEN '1' ELSE '2';
 
-SET &NIV_ACTIVITE = IF &SECTEUR EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN '1' ELSE '2';
-SET &NIV_ACTIVITE = IF &INDUCTEUR EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN &NIV_ACTIVITE ELSE '3';
 
-SET &LIB_ACTIVITE = IF '&NIV_ACTIVITE' EQ '1' THEN 'Tous les secteurs' ELSE IF &NIV_ACTIVITE EQ '2' THEN 'secteur' ELSE 'Inducteur';
 
-SET &NIV_STATUT = IF &STATUT EQ 'undefined' OR 'All' OR '_FOC_NULL' OR 'AARS17ST00TOUT' THEN '1' ELSE '2';
 
 
-*---------------------------------------------------
-* Récupération des codes axes selon les paramètres
-*---------------------------------------------------
-INCLUDE induct3_read_statut
 
TABLE FILE AXE_GEOAR
PRINT VALEUR_AXE
WHERE NIVEAU EQ '&GEO'
-IF &GEO EQ 1 THEN GOTO GEO_FIN ELSE IF &GEO EQ 2 THEN GOTO :GEO_N2;
-:GEO_N2
WHERE GEOAR_N2 EQ '&NEW_REGION'
-GOTO GEO_FIN
-GEO_FIN
ON TABLE HOLD AS TMP_AXE_GEO FORMAT ALPHA
END
 
 
-RUN
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_GEO='';
-REPEAT :BOUCLE31 WHILE &IORETURN EQ 0;
-READ TMP_AXE_GEO &GEO_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE31;
-SET &STR_GEO=IF &STR_GEO EQ '' THEN &GEO_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_GEO | '''' | ',' | '''' | &GEO_VAL | '''' ELSE &STR_GEO | ',' | '''' | &GEO_VAL | '''';
-SET &STR_GEO=IF &LINES EQ 1 THEN '''' | &STR_GEO | '''' ELSE &STR_GEO;
-SET &CPT=&CPT+1;
-:BOUCLE31
-CLOSE TMP_AXE_GEO
-TYPE &STR_GEO
 
 
TABLE FILE AXE_AARS17INDUCTEUR
PRINT VALEUR_AXE
WHERE NIVEAU EQ '3'
-IF &NIV_ACTIVITE EQ 1 THEN GOTO :ACTIVITE_N1;
-IF &NIV_ACTIVITE EQ 2 THEN GOTO :ACTIVITE_N2;
WHERE AARS17IND_N3 EQ '&INDUCTEUR'
-GOTO :ACTIVITE_N1
-:ACTIVITE_N2
WHERE AARS17IND_N2 EQ '&SECTEUR'
 
-:ACTIVITE_N1
ON TABLE HOLD AS TMP_AXE_AARS17INDUCTEUR32 FORMAT ALPHA
END
-RUN
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_ACT='';
-REPEAT :BOUCLE32 WHILE &IORETURN EQ 0;
-READ TMP_AXE_AARS17INDUCTEUR32 &ACT_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE32;
-SET &STR_ACT=IF &STR_ACT EQ '' THEN &ACT_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_ACT | '''' | ',' | '''' | &ACT_VAL | '''' ELSE &STR_ACT | ',' | '''' | &ACT_VAL | '''';
-SET &STR_ACT=IF &LINES EQ 1 THEN '''' | &STR_ACT | '''' ELSE &STR_ACT;
-SET &CPT=&CPT+1;
-:BOUCLE32
-CLOSE TMP_AXE_AARS17INDUCTEUR32
-TYPE &STR_ACT
 
 
-*---------------------------------
-* Total ETP selon les paramètres
-*---------------------------------
SET CDN=OFF
-RUN
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
	WHERE ( AX02_CODE IN ( &STR_GEO ) )
-*Filtre STATUT
	WHERE ( AX03_CODE IN ( &STR_STAT ) )
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
-*	BY AX04_LIBELLE
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS OUT FORMAT ALPHA
END
 
 
-*Test pour vérifier qu'il y a des valeurs
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
-RUN
 
-READ OUT &VALEUR_FINALE.20.
 
-GOTO :VAL
 
-:ERRMESS
-SET &VALEUR_FINALE=0;
 
-:VAL
 
-SET &VAL_FI=TRIM('L', &VALEUR_FINALE, 20, ' ', 1,'A20');
-SET &NEWVAL1=TRUNCATE(&VAL_FI);
SET CDN=SPACE
-RUN
 
-SET &NEWVAL=FTOA(&NEWVAL1, '(D10.1)', 'A12');
 
-TYPE NEWVAL = &NEWVAL
 
-*----------------------------
-* Récupération des libellés
-*----------------------------
 
-IF '&NEW_REGION' EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN GOTO TSNREG;
 
TABLE FILE AXE_GEOAR
PRINT LIBELLE
WHERE VALEUR_AXE EQ '&NEW_REGION'
ON TABLE HOLD AS TMP_LIB_GEO_NREG FORMAT ALPHA
END
-RUN
 
-READ TMP_LIB_GEO_NREG &NEW_REGION_LIB.A60.
-GOTO ENREG
-TSNREG
-SET &NEW_REGION_LIB='Toutes';
 
-ENREG
-TYPE &NEW_REGION_LIB
 
-SET &GEO_LIB = IF &GEO EQ 1 THEN 'National' ELSE IF &GEO EQ 2 THEN &NEW_REGION_LIB;
 
-IF &SECTEUR EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN GOTO TSSECTEUR;
 
TABLE FILE AXE_AARS17INDUCTEUR
PRINT LIBELLE
WHERE VALEUR_AXE EQ '&SECTEUR'
ON TABLE HOLD AS TMP_LIB_ACTIVITE_SECTEUR FORMAT ALPHA
END
-RUN
 
-READ TMP_LIB_ACTIVITE_SECTEUR &SECTEUR_LIB.A60.
-GOTO ESECTEUR
-TSSECTEUR
-SET &SECTEUR_LIB='Tous';
-ESECTEUR
-TYPE &SECTEUR_LIB
 
 
-IF &INDUCTEUR EQ 'undefined' OR 'All' OR '_FOC_NULL' THEN GOTO TSTYPE_INT;
 
TABLE FILE AXE_AARS17INDUCTEUR
PRINT LIBELLE
WHERE VALEUR_AXE EQ '&INDUCTEUR'
ON TABLE HOLD AS TMP_LIB_ACTIVITE_INDUCTEUR FORMAT ALPHA
END
-RUN
-READ TMP_LIB_ACTIVITE_INDUCTEUR &INDUCTEUR_LIB.A60.
-GOTO ETYPE_INT
-TSTYPE_INT
-SET &INDUCTEUR_LIB='Tous';
-ETYPE_INT
-TYPE &INDUCTEUR_LIB
 
 
CREATE FILE STOCK_PARAM_PDF
-RUN
 
MODIFY FILE STOCK_PARAM_PDF
FREEFORM LIB_NOM_PARAM VAL_PARAM
MATCH  LIB_NOM_PARAM
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA
'ARS:','&GEO_LIB',$
'Secteur:','&SECTEUR_LIB',$
'Inducteur:','&INDUCTEUR_LIB',$
'ETP &EXERCICE :','&NEWVAL',$
END
-RUN
 
DEFINE FILE STOCK_PARAM_PDF
CPT/I2 WITH LIB_NOM_PARAM = CPT + 1 ;
END
 
 
TABLE FILE STOCK_PARAM_PDF
PRINT LIB_NOM_PARAM AS '' VAL_PARAM AS '' CPT NOPRINT
ON TABLE SET PAGE-NUM NOLEAD
-*ON TABLE SET HTMLCSS ON
ON TABLE PCHOLD FORMAT PDF
ON TABLE SET STYLE *
TYPE=REPORT, SQUEEZE=ON,GRID=OFF,$
TYPE=DATA, SIZE=12, BACKCOLOR=( RGB(235 235 240) 'WHITE' ),$
TYPE=DATA, COLUMN=N1, FONT=ARIAL, COLOR=BLACK, STYLE=BOLD,$
TYPE=DATA, COLUMN=N2, FONT=ARIAL, COLOR=RGB(51 51 153),$
DEFMACRO=COND0001,MACTYPE=RULE,WHEN=N3 EQ 11,$
TYPE=DATA, COLUMN=N2, MACRO=COND0001, COLOR=RGB(255 102 0), SIZE=10, STYLE=BOLD,$
TYPE=DATA, COLUMN=N1, MACRO=COND0001, STYLE=BOLD,JUSTIFY=RIGHT,$
 
ENDSTYLE
END
