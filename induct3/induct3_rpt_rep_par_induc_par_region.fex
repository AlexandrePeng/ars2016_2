-* File induct3_rpt_rep_par_induct.fex
-*============================================================================
-* Projet Suivi d'activité ARS 2016 - Tableau de bord contrôleur de gestion
-*
-* Destinataire  : Philippe Louvel
-* Auteur        : Alexandre PENG
-* Date création : 12/08/2015
-* Description   : Enquête Activité ARS par Inducteur 2016
-*
-*----------------------------------------------------------------------------
-* Modifications
-*  Date   : jj/mm/ssaa
-*  Auteur : first last - sté
-*  Objet  : ...
-*============================================================================
-*Ajout du 0 avant les décimales lorque l'on a une valeur compris entre 0 et 1
SET CENT-ZERO = ON
SET CDN=SPACE
-*-SET &ECHO = ALL ;
-*-DEFAULTH &AX04_LIBELLE=''
 
 
-INCLUDE induct3_default
-INCLUDE induct3_read_statut
-INCLUDE induct3_read_geo
-INCLUDE induct3_read_activite
 
-IF &NEW_REGION EQ '_FOC_NULL' THEN GOTO :PGARDE;
-IF &SECTEUR EQ '_FOC_NULL' AND &INDUCTEUR EQ '_FOC_NULL' THEN GOTO :PGARDE;
 
 
-*****************************************************************************
-* Détermination de la règle de gestion correspondante
-*****************************************************************************
-*-SET &REGLE_GESTION=IF (&INDUCTEUR EQ '_FOC_NULL') AND (&SECTEUR EQ '_FOC_NULL') THEN '3' ELSE '1';
-SET &FILTRE_NIVEAU_GEO=IF (&NEW_REGION NE '_FOC_NULL') THEN '2' ELSE '1';
-SET &FILTRE_INDUCT=IF (&NEW_REGION NE '_FOC_NULL') THEN '2' ELSE '1';
 
 
-*****************************************************************************
-* Récupèrer la liste des inducteurs sélectionnés
-*****************************************************************************
TABLE FILE AXE_AARS17INDUCTEUR
PRINT VALEUR_AXE
WHERE NIVEAU EQ '3'
WHERE AARS17IND_N2 EQ '&SECTEUR'
WHERE AARS17IND_N3 EQ '&INDUCTEUR'
ON TABLE HOLD AS TMP_AXE_INDUCT_3 FORMAT ALPHA
END
 
-RUN
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_IND_NIV3='';
-REPEAT :BOUCLE4 WHILE &IORETURN EQ 0;
-READ TMP_AXE_INDUCT_3 &ACT_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE4;
-SET &STR_IND_NIV3=IF &STR_IND_NIV3 EQ '' THEN &ACT_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_IND_NIV3 | '''' | ',' | '''' | &ACT_VAL | '''' ELSE &STR_IND_NIV3 | ',' | '''' | &ACT_VAL | '''';
-SET &STR_IND_NIV3=IF &LINES EQ 1 THEN '''' | &STR_IND_NIV3 | '''' ELSE &STR_IND_NIV3;
-SET &CPT=&CPT+1;
-:BOUCLE4
-CLOSE TMP_AXE_INDUCT_3
-TYPE &STR_IND_NIV3
 
 
TABLE FILE AXE_AARS17INDUCTEUR
PRINT VALEUR_AXE
WHERE NIVEAU EQ '4'
WHERE AARS17IND_N2 EQ '&SECTEUR'
WHERE AARS17IND_N3 EQ '&INDUCTEUR'
ON TABLE HOLD AS TMP_AXE_INDUCT FORMAT ALPHA
END
 
-RUN
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_IND='';
-REPEAT :BOUCLE3 WHILE &IORETURN EQ 0;
-READ TMP_AXE_INDUCT &ACT_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE3;
-SET &STR_IND=IF &STR_IND EQ '' THEN &ACT_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_IND | '''' | ',' | '''' | &ACT_VAL | '''' ELSE &STR_IND | ',' | '''' | &ACT_VAL | '''';
-SET &STR_IND=IF &LINES EQ 1 THEN '''' | &STR_IND | '''' ELSE &STR_IND;
-SET &CPT=&CPT+1;
-:BOUCLE3
-CLOSE TMP_AXE_INDUCT
-TYPE &STR_IND
 
 
-*****************************************************************************
-* Récupère ETP
-*****************************************************************************
-*
TABLE FILE VIEW_IND_VAL_IA
 
SUM
	VALEUR_FINALE AS VALEUR_ETP
BY AX04_CODE_SUP
BY AX02_CODE
BY AX02_ORD
BY AX02_LIBELLE
BY AX04_CODE AS 'AX04_CODE_PER'
BY AX04_ORD
BY AX04_LIBELLE AS 'AX04_LIBELLE_PER'
 
-*Filtre INDICATEUR
    WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
 
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
 
-*Filtre GEO
	WHERE ( AX02_NIVEAU EQ ('&FILTRE_NIVEAU_GEO') )
-*Filtre STATUT
	WHERE ( AX03_CODE IN ( &STR_STAT ) )
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_IND ) )
 
	WHERE ( AX04_NIVEAU EQ '4')
-*
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_ETP
END
 
-*****************************************************************************
-* Récupère INDUCTEUR
-*****************************************************************************
-*
 
TABLE FILE VIEW_IND_VDB_IA
 
SUM
	VALEUR_VDB AS VALEUR_INDUCT
 
BY AX04_CODE
BY AX04_ORD
BY AX04_LIBELLE
BY AX02_CODE
BY AX02_ORD
BY AX02_LIBELLE
-*Filtre INDICATEUR
    WHERE ( ID_ELEMENT EQ '&ID_INDUCTEUR' )
 
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
 
-*Filtre GEO
	WHERE ( AX02_NIVEAU EQ ('&FILTRE_NIVEAU_GEO') )
 
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_IND_NIV3 ) )
 
	WHERE ( AX04_NIVEAU EQ '3')
-*
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_IND
END
 
JOIN CLEAR *
JOIN AX04_CODE_SUP AND AX02_CODE IN TMP_ETP TO UNIQUE AX04_CODE AND AX02_CODE IN TMP_IND
 
DEFINE FILE TMP_ETP
VALEUR_INDUCT2/D12 = IF VALEUR_INDUCT EQ '' THEN 0 ELSE VALEUR_INDUCT;
END
TABLE FILE TMP_ETP
SUM VALEUR_ETP
VALEUR_INDUCT2
COMPUTE V_NATIONAL/D10.1= VALEUR_INDUCT2 / VALEUR_ETP;
BY AX04_ORD
BY AX04_CODE_PER
BY AX04_LIBELLE_PER
ON TABLE HOLD AS TMP_FINAL
END
 
 
-*Filtre sur la region sélectionnée
 
 
 
TABLE FILE TMP_ETP
SUM VALEUR_ETP AS 'ETP'
VALEUR_INDUCT2 AS 'INDUCTEUR'
COMPUTE RATIO/D10.1= VALEUR_INDUCT2 / VALEUR_ETP;
-*Filtre GEO
WHERE ( AX02_CODE IN ( &STR_GEO ) )
BY AX04_ORD
BY AX04_CODE_PER
BY AX04_LIBELLE_PER
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_FINAL_REGION
END
 
JOIN CLEAR *
JOIN AX04_CODE_PER IN TMP_FINAL_REGION TO UNIQUE AX04_CODE_PER IN TMP_FINAL
 
 
TABLE FILE TMP_FINAL_REGION
SUM ETP/D10.1 AS 'ETP, correspondants'
INDUCTEUR AS 'Inducteurs'
RATIO AS 'Ratio'
V_NATIONAL AS 'Valeur nationale'
BY AX04_ORD NOPRINT
BY AX04_LIBELLE_PER AS 'Intitulés de l'inducteur et sa quotité'
ON TABLE SET ASNAMES ON
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE COLUMN-TOTAL AS 'TOTAL (100%)' 'ETP'
ON TABLE SET BYDISPLAY ON
ON TABLE SET HTMLCSS ON
ON TABLE SET CDN SPACE
ON TABLE PCHOLD FORMAT HTML
ON TABLE SET STYLE *
     INCLUDE = endeflt_inducteur,
 
 
$
TYPE=TITLE,
     COLUMN=N1,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     SIZE=10,
     COLOR='YELLOW',
     BACKCOLOR='PURPLE',
     STYLE=-UNDERLINE+BOLD,
$
TYPE=TITLE,
     COLUMN=N2,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     SIZE=10,
     COLOR='YELLOW',
     BACKCOLOR='PURPLE',
     STYLE=-UNDERLINE+BOLD,
$
TYPE=TITLE,
     COLUMN=N3,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     SIZE=10,
     COLOR='YELLOW',
     BACKCOLOR='PURPLE',
     STYLE=-UNDERLINE+BOLD,
$
TYPE=TITLE,
     COLUMN=N4,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     SIZE=10,
     COLOR='YELLOW',
     BACKCOLOR='PURPLE',
     STYLE=-UNDERLINE+BOLD,
$
TYPE=TITLE,
     COLUMN=N5,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     SIZE=10,
     COLOR='YELLOW',
     BACKCOLOR='PURPLE',
     STYLE=-UNDERLINE+BOLD,
$
TYPE=TITLE,
     COLUMN=N6,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=LIGHT,
     BORDER-LEFT=LIGHT,
     BORDER-RIGHT=LIGHT,
     SIZE=10,
     COLOR='YELLOW',
     BACKCOLOR='PURPLE',
     STYLE=-UNDERLINE+BOLD,
$
TYPE=GRANDTOTAL,
     SIZE=10,
     BACKCOLOR='NONE',
     STYLE=NORMAL,
     JUSTIFY=DEFAULT,
$
ENDSTYLE
 
 
END
 
-GOTO :FINI
-:ERRMESS
-HTMLFORM BEGIN
<TD>Message d'information<BR>
<BR>
<I><B>Cette requête n'a pas retourné de données.<BR>
</I></B></TD>
-HTMLFORM END
-GOTO :FINI
 
-:PGARDE;
-*Page de garde lorsque aucune nouvelles région ou région n'est sélectionnée
-HTMLFORM induct3_mire_accueil2
-:FINI
 
