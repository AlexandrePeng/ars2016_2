 File ars3_rpt_rep_par_induc.fex
============================================================================
 Projet Suivi d'activité ARS 2014 - Tableau de bord contrôleur de gestion
 
 Destinataire  : Philippe Louvel
 Auteur        : Nicolas Wattiaux (Information Builders France) / Guy Futé (Aixial BI)
 Date création : 12/08/2015
 Description   : Enquête Activité ARS par Statut
 
----------------------------------------------------------------------------
 Modifications
  Date   : jj/mm/ssaa
  Auteur : first last - sté
  Objet  : ...
============================================================================
-*Ajout du 0 avant les décimales lorque l'on a une valeur compris entre 0 et 1
SET CENT-ZERO = ON
-SET &ECHO = ALL ;
 
-DEFAULTH &AX04_LIBELLE=''
 
 
-SET &ID_INDICATEUR = 'I_GIDB001C';
-SET &ID_INDUCTEUR = 'I_GIDB0013';
-INCLUDE ars3_DEFAUT_ACT
 
-IF &REGION NE '_FOC_NULL' GOTO LVLREG;
-IF &NEW_REGION EQ '_FOC_NULL' THEN GOTO :PGARDE;
-SET &GEO=2;
 
-LVLREG
-SET &GEO=3;
-GOTO FLVL
 
-FLVL
TABLE FILE AXE_GEOAR
PRINT VALEUR_AXE
WHERE NIVEAU EQ &GEO
ON TABLE HOLD AS TMP_AXE_GEO FORMAT ALPHA
END
-RUN
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_GEO='';
-REPEAT :BOUCLE10 WHILE &IORETURN EQ 0;
-READ TMP_AXE_GEO &GEO_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE10;
-SET &STR_GEO=IF &STR_GEO EQ '' THEN &GEO_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_GEO | '''' | ',' | '''' | &GEO_VAL | '''' ELSE &STR_GEO | ',' | '''' | &GEO_VAL | '''';
-SET &STR_GEO=IF &LINES EQ 1 THEN '''' | &STR_GEO | '''' ELSE &STR_GEO;
-SET &CPT=&CPT+1;
-:BOUCLE10
-CLOSE TMP_AXE_GEO
 
-INCLUDE ars3_READ_ACTIVITE
 
-*****************************************************************************
-* Détermination de la règle de gestion correspondante
-*****************************************************************************
 
-SET &REGLE_GESTION=IF (&MISSION EQ '_FOC_NULL') AND (&TYPE_INT EQ '_FOC_NULL') AND (&SECTEUR EQ '_FOC_NULL') THEN '3' ELSE '1';
 
-*****************************************************************************
-* Création du rapport contrôleur de gestion
-*****************************************************************************
 
TABLE FILE VIEW_IND_VAL_IA
 
PRINT
	VALEUR_FINALE AS VALEUR_ARS
	AX02_ORD
	AX04_ORD
 
BY AX02_CODE
BY AX04_CODE
 
-*Filtre INDICATEUR
    WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
 
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
 
-*Filtre GEO
	WHERE ( AX02_CODE EQ (TMP_AXE_GEO) )
 
-*Filtre ENTITE
	WHERE ( AX03_NIVEAU EQ 1)
 
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
 
-*Filtre PLAFOND
	WHERE ( AX05_NIVEAU EQ 1 )
 
-*Filtre STATUT
	WHERE ( AX06_NIVEAU EQ 1 )
 
-*Filtre INSP_CONT
	WHERE ( AX07_NIVEAU EQ 1 )
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_FINAL FORMAT FOCUS INDEX AX02_CODE
 
END
 
-*Jointure
JOIN CLEAR *
JOIN
 AX02_CODE IN TMP_FINAL TO VALEUR_AXE IN AXE_GEOAR AS J1
END
JOIN
 AX04_CODE IN TMP_FINAL TO VALEUR_AXE IN AXE_AARS16DOM AS J3
END
 
DEFINE FILE TMP_FINAL
CLE_CONC/A75=AX02_CODE || AX04_CODE;
END
 
TABLE FILE TMP_FINAL
PRINT
VALEUR_ARS
-*AXE GEO
    GEOAR_N1_LIB
	GEOAR_N2_LIB
	GEOAR_N3_LIB
 
-*AXE ACTIVITE
	AARS16DOM_N1_LIB
	AARS16DOM_N2_LIB
	AARS16DOM_N3_LIB
	AARS16DOM_N4_LIB
 
-*Champs pour le tri
    GEOAR_N1_ORD
	GEOAR_N2_ORD
	GEOAR_N3_ORD
	AARS16DOM_N1_ORD
	AARS16DOM_N2_ORD
	AARS16DOM_N3_ORD
	AARS16DOM_N4_ORD
 
    BY CLE_CONC
    BY AX02_ORD
	BY AX02_CODE
	BY AX04_ORD
 
ON TABLE HOLD AS TMP_INTER FORMAT ALPHA
END
-RUN
 
 
DEFINE FILE VIEW_IND_VAL_IA
-*Dans le cas de la règle de gestion 1 la clé de jointure est la concaténation du code géo avec le code domaine d'activité
-IF &REGLE_GESTION NE '1' GOTO SKCLE1;
CLE_CONCAT/A75=AX02_CODE || AX03_CODE;
-GOTO ESKCLE
-SKCLE1
-*Dans le cas de la règle de gestion 2 ou 3 la clé de jointure est le code GEO correspondant
-IF &REGLE_GESTION NE '3' GOTO SKCLE2;
CLE_CONCAT/A75=AX02_CODE;
-SKCLE2
-ESKCLE
END
TABLE FILE VIEW_IND_VAL_IA
PRINT AX04_LIBELLE VALEUR_FINALE
BY CLE_CONCAT
BY AX02_CODE_SUP
WHERE ID_INDICATEUR EQ 'I_GIDB0013'
WHERE ( AX02_CODE EQ (TMP_AXE_GEO) )
 
-IF &REGLE_GESTION NE '1' GOTO SKCLE10;
WHERE AX03_CODE IN ( &STR_ACT )
-GOTO ESKCLE1
-SKCLE10
-IF &REGLE_GESTION NE '3' GOTO SKCLE20;
WHERE AX03_NIVEAU EQ 1
-GOTO ESKCLE1
-SKCLE20
-ESKCLE1
ON TABLE HOLD AS TMP_INDUC FORMAT ALPHA
END
-RUN
 
 
-READFILE TMP_INDUC
 
-IF &REGLE_GESTION NE '1' GOTO SKCLE11;
JOIN CLE_CONCAT IN TMP_INDUC TO ALL CLE_CONC IN TMP_INTER AS J7
-SKCLE11
-IF &REGLE_GESTION NE '2' OR '3' GOTO SKCLE21;
JOIN CLE_CONCAT IN TMP_INDUC TO ALL AX02_CODE IN TMP_INTER AS J7
-SKCLE21
 
 
TABLE FILE TMP_INDUC
 
PRINT
	AX04_LIBELLE
	VALEUR_FINALE
	VALEUR_ARS
	GEOAR_N2_LIB
	GEOAR_N3_LIB
 
-*AXE ACTIVITE
	AARS16DOM_N1_LIB
	AARS16DOM_N2_LIB
	AARS16DOM_N3_LIB
	AARS16DOM_N4_LIB
 
-*Champs pour le tri
    GEOAR_N1_ORD
	GEOAR_N2_ORD
	GEOAR_N3_ORD
	AARS16DOM_N1_ORD
	AARS16DOM_N2_ORD
	AARS16DOM_N3_ORD
	AARS16DOM_N4_ORD
    BY AX02_ORD NOPRINT
	BY AX04_ORD NOPRINT
	BY AX02_CODE_SUP
	BY AX02_CODE
 
 
ON TABLE HOLD AS TMP_FINAL_EXCEL
END
 
 
-*Test pour vérifier que des données sont présentes pour l'inducteur
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
 
DEFINE FILE TMP_FINAL_EXCEL
BLK/A1='';
FLAG_NR/A1= IF AX02_CODE_SUP EQ &NEW_REGION.QUOTEDSTRING  AND '&REGION' EQ '_FOC_NULL' THEN '*' ELSE IF AX02_CODE EQ &REGION.QUOTEDSTRING THEN '*' ELSE '';
END
 
TABLE FILE TMP_FINAL_EXCEL
FOOTING
"(1) Le type d'inducteur est: &AX04_LIBELLE"
 
SUM
 VALEUR_ARS/D10.2 AS 'Total ETP'
 VALEUR_FINALE/D12 AS 'Inducteur (1)'
 COMPUTE RATIO/D10= VALEUR_FINALE / VALEUR_ARS;
 
	BY BLK NOPRINT
	BY TOTAL HIGHEST VALEUR_FINALE NOPRINT
	BY FLAG_NR AS ''
 
-IF &REGION NE '_FOC_NULL' THEN GOTO LIBREGGEO;
	BY	GEOAR_N2_ORD NOPRINT
	BY GEOAR_N2_LIB AS 'Nouvelles Régions'
-GOTO :ENDLIBGEO;
-LIBREGGEO
-IF &GEO EQ 2 THEN GOTO :ENDLIBGEO;
	BY	GEOAR_N3_ORD NOPRINT
	BY GEOAR_N3_LIB AS 'Régions'
 
-:ENDLIBGEO
 
 
-*AXE ACTIVITE
-IF &ACTIVITE EQ 1 THEN GOTO :ENDLIBACTIVITE;
	BY AARS16DOM_N2_ORD NOPRINT
	BY AARS16DOM_N2_LIB AS 'Secteur'
-IF &ACTIVITE EQ 2 THEN GOTO :ENDLIBACTIVITE;
	BY AARS16DOM_N3_ORD NOPRINT
	BY AARS16DOM_N3_LIB AS 'Type d'intervention'
-IF &ACTIVITE EQ 3 THEN GOTO :ENDLIBACTIVITE;
	BY AARS16DOM_N4_ORD NOPRINT
	BY AARS16DOM_N4_LIB AS 'Mission'
-:ENDLIBACTIVITE;
 
-:NORULE;
 
ON BLK RECOMPUTE AS 'TOTAL'
ON TABLE NOTOTAL
ON TABLE SET PAGE-NUM OFF
ON TABLE SET BYDISPLAY OFF
ON TABLE SET HTMLCSS ON
ON TABLE SET CDN SPACE
ON TABLE PCHOLD FORMAT HTML
ON TABLE SET STYLE *
     INCLUDE = endeflt,
$
TYPE=REPORT, SQUEEZE=ON,GRID=ON,BORDER-TOP=LIGHT,BORDER-LEFT=LIGHT,BORDER-RIGHT=LIGHT,BORDER-BOTTOM=LIGHT, SIZE= 10,$
TYPE=FOOTING,GRID=OFF,BORDER=OFF,SIZE=10,JUSTIFY=LEFT,$
TYPE=DATA,JUSTIFY=LEFT,$
TYPE=GRANDTOTAL, BACKCOLOR=RGB(184 204 228),STYLE=BOLD,JUSTIFY=CENTER,$
TYPE=SUBTOTAL,BY=1,JUSTIFY=LEFT,$
TYPE=TITLE,COLUMN=N6,JUSTIFY=CENTER,$
TYPE=TITLE,COLUMN=N7,JUSTIFY=CENTER,$
TYPE=TITLE,COLUMN=N8,JUSTIFY=CENTER,$
TYPE=TITLE,BACKCOLOR=RGB(184 204 228),$
TYPE=TITLE,COLUMN=N5,BORDER-LEFT=OFF,$
TYPE=DATA,COLUMN=N5,BORDER-LEFT=OFF,$
GRAPHTYPE=DATA,COLUMN=N7,GRAPHCOLOR='FUCHSIA',$
GRAPHTYPE=DATA,COLUMN=N8,GRAPHCOLOR='NAVY',$
TYPE=SUBTOTAL,
     STYLE=BOLD,
	 BACKCOLOR=RGB(184 204 228),
$
ENDSTYLE
 
 
END
 
-GOTO :FINI
-:ERRMESS
-HTMLFORM BEGIN
<TD>Message d'information<BR>
<BR>
<I><B>Cette requête n'a pas retourné de données.<BR>
</I></B></TD>
-HTMLFORM END
-GOTO :FINI
 
-:PGARDE;
-*Page de garde lorsque aucune nouvelles région ou région n'est sélectionnée
-HTMLFORM ars3_INDUCT_MIRE_ACCUEIL
-:FINI
 
