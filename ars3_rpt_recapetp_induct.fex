-* File dir2_rpt_recapetp_induct.fex
-*==================================================================================================
-* Projet Suivi d'activité 2014 - Inducteur de charges : Récapitulatif des paramètres, Total ETP et inducteur
-*
-* Destinataire  : Philippe Louvel
-* Auteur        : Nicolas Wattiaux (Information Builders), Guy Fute (Aixial BI)
-* Date création : 01/07/2015
-* Description   :
-*
-*--------------------------------------------------------------------------------------------------
-* Modifications
-*  Date   : jj/mm/ssaa
-*  Auteur : first last - sté
-*  Objet  : ...
-*==================================================================================================
SET SPACES =ON;
 
-DEFAULTH &AX04_LIBELLE = ''
-DEFAULTH &VALEUR_FINALE = 0
-INCLUDE ars3_DEFAUT_ACT
 
-DEFAULT &FMT_SORTIE='HTML'
 
-*---------------------------------------------------
-* Récupération des codes axes selon les paramètres
-*---------------------------------------------------
 
-INCLUDE ars3_READ_GEO
-INCLUDE ars3_READ_ACTIVITE
 
-*---------------------------------
-* Total ETP selon les paramètres
-*---------------------------------
SET CDN=OFF
-RUN
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE
 
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
	WHERE ( AX02_CODE IN ( &STR_GEO ) )
-*Filtre ENTITE
	WHERE  AX03_NIVEAU EQ 1
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
-*Filtre PLAFOND
	WHERE  AX05_NIVEAU EQ 1
-*Filtre STATUT
	WHERE  AX06_NIVEAU EQ 1
-*Filtre INSPECTION CONTROLE (sinon la valeur restituée est doublée)
	WHERE  AX07_NIVEAU EQ 1
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS OUT FORMAT ALPHA
END
 
-*Test pour vérifier qu'il y a des valeurs
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
-RUN
 
-READ OUT &VALEUR_FINALE.20.
 
-GOTO :VAL
 
-:ERRMESS
-SET &VALEUR_FINALE=0;
 
-:VAL
 
-SET &VAL_FI=TRIM('L', &VALEUR_FINALE, 20, ' ', 1,'A20');
-SET &NEWVAL1=TRUNCATE(&VAL_FI);
SET CDN=SPACE
-RUN
 
-SET &NEWVAL=FTOA(&NEWVAL1, '(D10.1)', 'A12');
 
 
-*Récupération inducteur
 
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE/P20.1
BY AX04_LIBELLE
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDUCTEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
	WHERE ( AX02_CODE IN ( &STR_GEO ) )
-*Filtre ACTIVITE
	WHERE ( AX03_CODE IN ( &STR_ACT ))
 
 
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS RECAP_INDUCT FORMAT ALPHA
END
 
-RUN
-IF &LINES EQ 0 THEN GOTO :NOREAD; 
 
-READFILE RECAP_INDUCT
 
-SET &VAL_FI2=TRIM('L', &VALEUR_FINALE, 20, ' ', 1,'A20');
-SET &NEWVAL2=TRUNCATE(&VAL_FI2);
SET CDN=SPACE
-RUN
 
-SET &NEWVAL3=FTOA(&NEWVAL2, '(D10.1)', 'A12');
-GOTO :AFTERNEWVAL;
-:NOREAD;
-SET &AX04_LIBELLE= 'Aucun inducteur associé';
-SET &NEWVAL3=0;
-:AFTERNEWVAL
-*
-*----------------------------
-* Récupération des libellés
-*----------------------------
-INCLUDE ars3_RECUP_LIB_GEO
-INCLUDE ars3_RECUP_LIB_ACTIVITE
 
 
-HTMLFORM BEGIN
<HTML>
<BODY style="Z-INDEX: 0; OVERFLOW: hidden;background:#5472AE">
 
<font color="white">
<font face="arial">
-*<font face="verdana">
<font size=2>
<center>
<p>
<b><font color="black">ARS:</font>ARS &GEO_LIB</b>
<br>
<b><font color="black">Secteur:</font>&SECTEUR_LIB</b>
<br>
<b><font color="black">Type d'intervention:</font>&TYPE_INT_LIB</b>
<br>
<b><font color="black">Mission:</font>&MISSION_LIB</b>
<br>
<font size=5> <font size=3>  ETP &EXERCICE : </font> <font color="#ED7F10"> <b>&NEWVAL</b>
</font> <font size=3>  &AX04_LIBELLE : </font> <font color="#ED7F10"> <b>&NEWVAL3</b> </font>
</p>
</center></font></body></html>
-HTMLFORM END
-GOTO :FINI
 
 
-:FINI
 
 
 
 
