-* File ars3_rpt_rep_struct_emploi2016.fex
-*============================================================================
-* Projet Suivi d'activité ARS 2016
-*
-* Destinataire  : Philippe Louvel
-* Auteur        :  Guy Futé (Information Builders France)
-* Date création : 01/12/2016
-* Description   : Enquête Activité ARS par Statut
-*
-*----------------------------------------------------------------------------
-* Modifications
-*  Date   : jj/mm/ssaa
-*  Auteur : first last - sté
-*  Objet  : ...
-*============================================================================
-*-*Ajout du 0 avant les décimales lorque l'on a une valeur compris entre 0 et 1
 
-SET &ECHO = ALL ;
-DEFAULTH &GEOAR_N2='_FOC_NULL'
-DEFAULTH &AX02_LIBELLE=''
-*SET NODATA=0
SET CENT-ZERO = ON
-TYPE ============================================================================
-TYPE Procédure    : ars3_rpt_rep_struct_emploi2016.fex
-TYPE ============================================================================
 
-*------------------------------------------------------------------
-* Definition du passage de paramètre pour Total ETP et % du total
-*------------------------------------------------------------------
-SET &ID_INDICATEUR= 'I_GIDB0015';
-SET &ID_INDUCTEUR= 'D_GIDB0016';
-SET &ID_RANKING='D_GIDB0017';
-SET &EXERCICE= 2015 ;
-INCLUDE ars3_DEFAUT_STAT
-*Renvoie à la page de garde lorsqu'aucun contrôle de l'axe géo n'est sélectionné
-IF &NEW_REGION EQ '_FOC_NULL' AND &REGION EQ '_FOC_NULL' THEN GOTO :PGARDE;
-IF &SECTEUR EQ '_FOC_NULL' THEN GOTO :PGARDE;
 
 
-**************************Récupération de la région ou de la nouvelle région enfonction des sélections sur l'axe géographique
-IF &NEW_REGION EQ '_FOC_NULL' AND &REGION NE '_FOC_NULL' THEN GOTO :CALCGEO;
-IF &NEW_REGION NE '_FOC_NULL' AND &REGION NE '_FOC_NULL' THEN GOTO :CALCGEO;
 
TABLE FILE AXE_GEOAR
SUM GEOAR_N2
WHERE GEOAR_N2 EQ &NEW_REGION.QUOTEDSTRING
ON TABLE HOLD AS TMP_AXE_GEO FORMAT ALPHA
END
-RUN
-READ TMP_AXE_GEO &STR_GEO.A15.
-GOTO :FINCALCGEO;
 
-:CALCGEO
TABLE FILE AXE_GEOAR
SUM GEOAR_N3
WHERE GEOAR_N3 EQ &REGION.QUOTEDSTRING
ON TABLE HOLD AS TMP_AXE_GEO FORMAT ALPHA
END
-RUN
-READ TMP_AXE_GEO &STR_GEO.A15.
 
-:FINCALCGEO
 
-**********************Récupération des secteurs sélectionnés
-SET &NIV_ACTIVITE=IF &SECTEUR NE '_FOC_NULL' THEN '2' ELSE '1';
TABLE FILE AXE_AARS16INDUCTEUR
PRINT VALEUR_AXE
WHERE NIVEAU EQ &NIV_ACTIVITE.QUOTEDSTRING
-IF &NIV_ACTIVITE EQ 1 THEN GOTO :ACTIVITE_N1;
WHERE AARS16IND_N2 EQ &SECTEUR.QUOTEDSTRING
-:ACTIVITE_N1
ON TABLE HOLD AS TMP_AXE_AARS16INDUCTEUR FORMAT ALPHA
END
-RUN
 
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_ACT='';
-REPEAT :BOUCLE2 WHILE &IORETURN EQ 0;
-READ TMP_AXE_AARS16INDUCTEUR &ACT_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE2;
-SET &STR_ACT=IF &STR_ACT EQ '' THEN &ACT_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_ACT | '''' | ',' | '''' | &ACT_VAL | '''' ELSE &STR_ACT | ',' | '''' | &ACT_VAL | '''';
-SET &STR_ACT=IF &LINES EQ 1 THEN '''' | &STR_ACT | '''' ELSE &STR_ACT;
-SET &CPT=&CPT+1;
-:BOUCLE2
-CLOSE TMP_AXE_AARS16INDUCTEUR
-TYPE &STR_ACT
 
-*******************récupération de la liste des régions ou nouvelles régions selon leur rang
 
-DEFAULTH &VALEUR_VDB='';
-DEFAULTH &AX02_LIBELLE='';
TABLE FILE VIEW_IND_VDB_IA
SUM VALEUR_VDB
BY AX02_CODE
BY AX02_LIBELLE
WHERE AX01_CODE EQ '2015'
WHERE ID_ELEMENT EQ 'D_GIDB0017'
WHERE AX02_NIVEAU EQ &GEO
WHERE AX04_CODE IN (&STR_ACT)
ON TABLE HOLD AS TMP_LIST_RANKING FORMAT ALPHA
END
 
-*******************récupération du rang de la région ou nouvelle région sélectionnée
TABLE FILE TMP_LIST_RANKING
SUM AX02_CODE
BY VALEUR_VDB/I2
WHERE AX02_CODE EQ '&STR_GEO'
ON TABLE HOLD AS TMP_RANKING_CODE FORMAT ALPHA
END
-RUN
 
-IF &LINES EQ 0 THEN GOTO NORANKING;
 
-READFILE TMP_RANKING_CODE
-RUN
 
-*******************récupération du libellé de la région ou nouvelle région sélectionnée
TABLE FILE TMP_LIST_RANKING
SUM AX02_LIBELLE
BY VALEUR_VDB/I2
WHERE AX02_CODE EQ '&STR_GEO'
ON TABLE HOLD AS TMP_RANKING_LIBELLE
END
-RUN
-READFILE TMP_RANKING_LIBELLE
-RUN
-**************************************Initialisation des rangs à récupérer par rapport à la valeur géographique sélectionnée dans le cas général*******************
-SET &RANK_PLUS1 = &VALEUR_VDB + 1;
-SET &RANK_PLUS2 = &VALEUR_VDB + 2;
-SET &RANK_MOINS1 = &VALEUR_VDB - 1;
-SET &RANK_MOINS2 = &VALEUR_VDB - 2;
-****************************************Cas particuliers***********************
-******************************Cas 1: test afin de déterminer s'il s'agit du dernier élément de la liste************
TABLE FILE TMP_LIST_RANKING
SUM VALEUR_VDB
 AX02_LIBELLE
 BY AX02_CODE
WHERE ( VALEUR_VDB EQ &RANK_PLUS1 )
ON TABLE SET ASNAMES ON
ON TABLE SAVE FORMAT HTML
END
-RUN
-IF &LINES EQ 0 THEN GOTO LASTGEO;
 
-******************************Cas 2: test afin de déterminer s'il s'agit de l'avant dernier élément de la liste************
TABLE FILE TMP_LIST_RANKING
SUM VALEUR_VDB
 AX02_LIBELLE
 BY AX02_CODE
WHERE ( VALEUR_VDB EQ &RANK_PLUS2 )
ON TABLE SET ASNAMES ON
ON TABLE SAVE FORMAT HTML
END
-RUN
-IF &LINES EQ 0 THEN GOTO BEFORELASTGEO;
 
-******************************Cas 3:test afin de déterminer s'il s'agit du premier élément de la liste************
TABLE FILE TMP_LIST_RANKING
SUM VALEUR_VDB
 AX02_LIBELLE
 BY AX02_CODE
WHERE ( VALEUR_VDB EQ &RANK_MOINS1 )
ON TABLE SET ASNAMES ON
ON TABLE SAVE FORMAT HTML
END
-RUN
-IF &LINES EQ 0 THEN GOTO FIRSTGEO;
 
-******************************Cas 4: test afin de déterminer s'il s'agit du 2e élément de la liste************
TABLE FILE TMP_LIST_RANKING
SUM VALEUR_VDB
 AX02_LIBELLE
 BY AX02_CODE
WHERE ( VALEUR_VDB EQ &RANK_MOINS2 )
ON TABLE SET ASNAMES ON
ON TABLE SAVE FORMAT HTML
END
-RUN
-IF &LINES EQ 0 THEN GOTO SECONDGEO;
 
-GOTO FININIT;
 
 
-LASTGEO
-SET &VALEUR_VDB = &VALEUR_VDB -2;
-GOTO ININTIAL_VAL;
 
-BEFORELASTGEO
-SET &VALEUR_VDB = &VALEUR_VDB -1;
-GOTO ININTIAL_VAL;
 
-FIRSTGEO
-SET &VALEUR_VDB = &VALEUR_VDB +2;
-GOTO ININTIAL_VAL;
 
-SECONDGEO
-SET &VALEUR_VDB = &VALEUR_VDB +1;
-GOTO ININTIAL_VAL;
 
-*******************Récupération de la région ou nouvelle sélectionnée ainsi que 4 régions ou nouvelles régions l'entourant
-ININTIAL_VAL
-SET &RANK_PLUS1 = &VALEUR_VDB + 1;
-SET &RANK_PLUS2 = &VALEUR_VDB + 2;
-SET &RANK_MOINS1 = &VALEUR_VDB - 1;
-SET &RANK_MOINS2 = &VALEUR_VDB - 2;
 
-FININIT
TABLE FILE TMP_LIST_RANKING
SUM VALEUR_VDB
 AX02_LIBELLE
 BY AX02_CODE AS 'GEO_VAL'
WHERE ( VALEUR_VDB EQ &VALEUR_VDB ) OR ( VALEUR_VDB EQ &RANK_PLUS1 )  OR ( VALEUR_VDB EQ &RANK_PLUS2 ) OR ( VALEUR_VDB EQ &RANK_MOINS1 ) OR ( VALEUR_VDB EQ &RANK_MOINS2 )
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_GEO_FINAL FORMAT ALPHA
END
-RUN
 
 
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_GEO='';
-REPEAT :BOUCLE1 WHILE &IORETURN EQ 0;
-READ TMP_GEO_FINAL &GEO_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE1;
-SET &STR_GEO=IF &STR_GEO EQ '' THEN &GEO_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_GEO | '''' | ',' | '''' | &GEO_VAL | '''' ELSE &STR_GEO | ',' | '''' | &GEO_VAL | '''';
-SET &STR_GEO=IF &LINES EQ 1 THEN '''' | &STR_GEO | '''' ELSE &STR_GEO;
-SET &CPT=&CPT+1;
-:BOUCLE1
-CLOSE TMP_AXE_GEO
-TYPE &STR_GEO
-:FINCALCGEO
-RUN
 
-**********************Récupération de libellés des 5 régions ou nouvelles régions à afficher sous forme de variables distinctes ************************************
-DEFAULTH &CHECKGEO_SELECT='';
-DEFAULTH &CHECKGEO_MOINS1='';
-DEFAULTH &CHECKGEO_MOINS2='';
-DEFAULTH &CHECKGEO_PLUS1='';
-DEFAULTH &CHECKGEO_PLUS2='';
 
TABLE FILE TMP_LIST_RANKING
PRINT AX02_LIBELLE AS 'CHECKGEO_SELECT'
WHERE VALEUR_VDB EQ &VALEUR_VDB
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS CHECKGEO_SELECT
END
-RUN
-READFILE CHECKGEO_SELECT
 
-RUN
-*****
TABLE FILE TMP_LIST_RANKING
PRINT AX02_LIBELLE AS 'CHECKGEO_MOINS1'
WHERE VALEUR_VDB EQ &RANK_MOINS1
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS CHECKGEO_MOINS1
END
-RUN
-READFILE CHECKGEO_MOINS1
-RUN
 
-*****
TABLE FILE TMP_LIST_RANKING
PRINT AX02_LIBELLE AS 'CHECKGEO_MOINS2'
WHERE VALEUR_VDB EQ &RANK_MOINS2
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS CHECKGEO_MOINS2
END
-RUN
-READFILE CHECKGEO_MOINS2
-RUN
 
-*****
TABLE FILE TMP_LIST_RANKING
PRINT AX02_LIBELLE AS 'CHECKGEO_PLUS1'
WHERE VALEUR_VDB EQ &RANK_PLUS1
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS CHECKGEO_PLUS1
END
-RUN
-READFILE CHECKGEO_PLUS1
-RUN
 
-*****
TABLE FILE TMP_LIST_RANKING
PRINT AX02_LIBELLE AS 'CHECKGEO_PLUS2'
WHERE VALEUR_VDB EQ &RANK_PLUS2
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS CHECKGEO_PLUS2
END
-RUN
-READFILE CHECKGEO_PLUS2
-RUN
 
 
-*******************************************************************************************************************************************************************************************
 
DEFINE FILE VIEW_IND_VAL_IA
VALEUR_FIN/D20.2= IF VALEUR_FINALE IS MISSING THEN 0 ELSE VALEUR_FINALE ;
END
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FIN
BY AX02_CODE
BY AX02_LIBELLE
BY AX03_ORDRE
BY AX03_CODE
BY AX03_LIBELLE
-*Filtre INDICATEUR
	WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
-*Filtre GEO
	WHERE ( AX02_CODE IN ( &STR_GEO ) )
-IF &GEO EQ 3 THEN GOTO NIVGEO3 ;
WHERE ( AX02_NIVEAU EQ 2)
-GOTO FINNIVGEO
-NIVGEO3
WHERE ( AX02_NIVEAU EQ 3)
-FINNIVGEO
-*Filtre STATUT
 
-IF &STATUT NE 'STATUT' THEN GOTO WHERENIV3;
WHERE ( AX03_NIVEAU EQ 2 )
-GOTO WHERENIVFIN
-WHERENIV3
 
WHERE ( AX03_NIVEAU EQ 3 )
-WHERENIVFIN
-*Filtre ACTIVITE
	WHERE ( AX04_CODE IN ( &STR_ACT ) )
ON TABLE SET ASNAMES ON
ON TABLE SET ASNAME ON
ON TABLE HOLD AS TMP_FNEWREG FORMAT ALPHA
END
-RUN
 
 
-INCLUDE ars3_NODATA
 
JOIN CLEAR *
JOIN AX02_CODE IN TMP_LIST_RANKING TO MULTIPLE AX02_CODE IN TMP_FNEWREG AS J1;
JOIN AX03_CODE IN TMP_LIST_RANKING TO UNIQUE VALEUR_AXE IN AXE_AARS16STATUT AS J2;
END
-RUN
 
 
TABLE FILE TMP_LIST_RANKING
SUM PCT.VALEUR_FIN AS 'PCT_NREG'
BY AARS16STATUT_N2_ORD
BY AARS16STATUT_N2_LIB AS ''
BY AARS16STATUT_N3_ORD
BY AARS16STATUT_N3_LIB AS ''
ACROSS VALEUR_VDB NOPRINT
ACROSS AX02_CODE NOPRINT
ACROSS AX02_LIBELLE AS 'geo'
ON TABLE HOLD AS TMP_LIST_RANKING2
END
-RUN
 
DEFINE FILE TMP_LIST_RANKING2
BLANK/A1='';
END
TABLE FILE TMP_LIST_RANKING2
PRINT
 E01/A55 NOPRINT
 E02/A55 AS ''
-IF &STATUT EQ 'STATUT' THEN GOTO :SUITESTAT;
 E03/A55 NOPRINT
 E04/A55 AS ''
-:SUITESTAT
 E05/D3% AS 'Région &CHECKGEO_MOINS2'
 E06/D3% AS 'Région &CHECKGEO_MOINS1'
 E07/D3% AS 'Région &CHECKGEO_SELECT'
 E08/D3% AS 'Région &CHECKGEO_PLUS1'
 E09/D3% AS 'Région &CHECKGEO_PLUS2'
 BY BLANK NOPRINT
 BY E07
-IF &STATUT EQ 'STATUT' THEN GOTO :FINSUITESTAT;
-*BY AARS16STATUT_N2_ORD NOPRINT
-*BY AARS16STATUT_N3_ORD NOPRINT
ON BLANK SUBFOOT
"TOTAL<+0>100%<+0>100%<+0>100%<+0>100%<+0>100%<+0>"
-GOTO :ONTABLE;
 
-:FINSUITESTAT
 
BY AARS16STATUT_N2_ORD NOPRINT
ON BLANK SUBFOOT
"TOTAL<+0>100%<+0>100%<+0>100%<+0>100%<+0>100%<+0>"
 
-:ONTABLE
ON TABLE SET ASNAMES ON
ON TABLE NOTOTAL
ON TABLE SET PAGE-NUM OFF
ON TABLE SET BYDISPLAY ON
ON TABLE SET HTMLCSS ON
ON TABLE SET CDN SPACE
ON TABLE PCHOLD FORMAT HTML
ON TABLE SET STYLE *
     INCLUDE = endeflt,
$
-IF &STATUT EQ 'STATUT' THEN GOTO :STYLEN2;
TYPE=REPORT, SQUEEZE=ON,GRID=ON,BORDER-TOP=LIGHT,BORDER-LEFT=LIGHT,BORDER-RIGHT=LIGHT,BORDER-BOTTOM=LIGHT, SIZE= 10,$
TYPE=REPORT, COLUMN=N5, JUSTIFY=LEFT,SQUEEZE=3,$
TYPE=REPORT, COLUMN=N7, JUSTIFY=LEFT,SQUEEZE=3,$
TYPE=REPORT, COLUMN=N8, JUSTIFY=LEFT,SQUEEZE=1.5,$
TYPE=REPORT, COLUMN=N9, JUSTIFY=LEFT,SQUEEZE=1.5,$
TYPE=REPORT, COLUMN=N10, JUSTIFY=LEFT,SQUEEZE=1.5,$
TYPE=REPORT, COLUMN=N11, JUSTIFY=LEFT,SQUEEZE=1.5,$
TYPE=REPORT, COLUMN=N12, JUSTIFY=LEFT,SQUEEZE=1.5,$
TYPE=TITLE,JUSTIFY=CENTER,BACKCOLOR=RGB(184 204 228),$
TYPE=TITLE,COLUMN=N5,BORDER-LEFT=OFF,BORDER-TOP=OFF,BACKCOLOR=RGB(255 255 255),$
TYPE=TITLE,COLUMN=N7,BORDER-LEFT=OFF,BORDER-TOP=OFF,BACKCOLOR=RGB(255 255 255),$
GRAPHTYPE=DATA,COLUMN=N8,GRAPHCOLOR='FUCHSIA',$
GRAPHTYPE=DATA,COLUMN=N9,GRAPHCOLOR='BLUE',$
GRAPHTYPE=DATA,COLUMN=N10,GRAPHCOLOR='LIME',$
GRAPHTYPE=DATA,COLUMN=N11,GRAPHCOLOR='PURPLE',$
GRAPHTYPE=DATA,COLUMN=N12,GRAPHCOLOR='YELLOW',$
 
TYPE=SUBFOOT,
     BACKCOLOR=RGB(184 204 228),
     HEADALIGN=BODY,
	 SIZE= 10,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=1,
     SIZE=10,
     COLSPAN=1,
     STYLE=BOLD,
     JUSTIFY=LEFT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=2,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=3,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=4,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=5,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=6,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=7,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
 
ENDSTYLE
END
-GOTO FINTAB;
 
-:STYLEN2
TYPE=REPORT, SQUEEZE=ON,GRID=ON,BORDER-TOP=LIGHT,BORDER-LEFT=LIGHT,BORDER-RIGHT=LIGHT,BORDER-BOTTOM=LIGHT, SIZE= 10,$
TYPE=TITLE,JUSTIFY=CENTER,BACKCOLOR=RGB(184 204 228),$
TYPE=TITLE,COLUMN=N4,BORDER-LEFT=OFF,BORDER-TOP=OFF,BACKCOLOR=RGB(255 255 255),$
GRAPHTYPE=DATA,COLUMN=N5,GRAPHCOLOR='FUCHSIA',$
GRAPHTYPE=DATA,COLUMN=N6,GRAPHCOLOR='BLUE',$
GRAPHTYPE=DATA,COLUMN=N7,GRAPHCOLOR='LIME',$
GRAPHTYPE=DATA,COLUMN=N8,GRAPHCOLOR='PURPLE',$
GRAPHTYPE=DATA,COLUMN=N9,GRAPHCOLOR='YELLOW',$
 
TYPE=SUBFOOT,
     BACKCOLOR=RGB(184 204 228),
     HEADALIGN=BODY,
	 SIZE= 10,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=1,
     SIZE=10,
     COLSPAN=1,
     STYLE=BOLD,
     JUSTIFY=LEFT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=2,
     SIZE=10,
     COLSPAN=1,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=3,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=4,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=5,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
TYPE=SUBFOOT,
     LINE=1,
     OBJECT=TEXT,
     ITEM=6,
     SIZE=10,
     COLSPAN=2,
     STYLE=BOLD,
     JUSTIFY=RIGHT,
$
ENDSTYLE
 
END
 
-GOTO :FINI
 
-NORANKING
-*Page lorsqu'il n'y a pas de rang pour la valeur Géo sélectionnée
-HTMLFORM ars3_NORANKING
-GOTO :FINI
-FINTAB
-GOTO :FINI
-:PGARDE;
-*Page de garde lorsque aucune nouvelles région ou région n'est sélectionnée
-HTMLFORM ars3_SE_MIRE_ACCUEIL
-:FINI
 
 
 
 
