-*************************************************************SUITE***********************************************************************************************************************************
-* File: IBFS:/Dev/EDA/EDASERVE/APPPATH/baseapp/Procedure2.fex Created by WebFOCUS AppStudio
-* File ars3_dom_tom.fex
-*============================================================================
-* Projet Suivi d'activité ARS 2014
-*
-* Destinataire  : Philippe Louvel
-* Auteur        : Guy FUTE (Information builders), Nicolas WATTIAUX (Information builders)
-* Date création : 10/2015
-* Description   : Procédure permettant de gérer les différents calculs de la carto DOM TOM de l'onglet activité
-*
-*----------------------------------------------------------------------------
-* Modifications
-*  Date   : 10/2015
-*  Auteur : Guy FUTE
 
-*============================================================================
-* Procedure    : ars3_dom_tom.fex
-* ------------------------------------------------------------------------------------------------
 
-SET &ECHO=ALL;
SET MSG=ON
-RUN
-DEFAULTH &ETPNAT=0
-DEFAULTH &MINI=100
-DEFAULTH &LEPAS=1
-DEFAULTH &NEW_REGION='_FOC_NULL'
-DEFAULTH &REGION ='_FOC_NULL'
-DEFAULTH &STATUT='_FOC_NULL'
-DEFAULTH &DEPARTEMENT ='_FOC_NULL'
-DEFAULTH &SECTEUR ='_FOC_NULL'
-DEFAULTH &TYPE_INT ='_FOC_NULL'
-DEFAULTH &MISSION ='_FOC_NULL'
 
 
-SET &INDIC = '% ETP' ;
-SET &ID_INDICATEUR='I_GIDB001C';
-*Récupérer le nb de d'occurences choisies ( 6 par défaut )
-SET &NB_DOMTOM = 6 ;
-* Récupération du niveau Geo sélectionné dans les filtres pour le dénominateur du calcul du ratio
-SET &NIVEAU_GEO=IF (&NEW_REGION NE '_FOC_NULL') AND (&REGION EQ '_FOC_NULL') AND (&DEPARTEMENT EQ '_FOC_NULL') THEN 2 ELSE IF (&REGION NE '_FOC_NULL') AND (&DEPARTEMENT EQ '_FOC_NULL') THEN 3 ELSE IF &DEPARTEMENT NE '_FOC_NULL' THEN 4 ELSE 1;
-SET &&NIVEAU_GEO_NIC=IF (&NEW_REGION NE '_FOC_NULL') AND (&REGION EQ '_FOC_NULL') AND (&DEPARTEMENT EQ '_FOC_NULL') THEN 2 ELSE IF (&REGION NE '_FOC_NULL') AND (&DEPARTEMENT EQ '_FOC_NULL') THEN 3 ELSE IF &DEPARTEMENT NE '_FOC_NULL' THEN 4 ELSE 2;
-SET &NIVEAU_GEO_PLUS1= IF &NEW_REGION EQ '_FOC_NULL' AND &REGION EQ '_FOC_NULL' AND &DEPARTEMENT EQ '_FOC_NULL' THEN &NIVEAU_GEO+1 ELSE &NIVEAU_GEO;
 
 
 
-*********Liste des départements (DOM TOM)
TABLE FILE AXE_GEOAR
PRINT VALEUR_AXE
WHERE NIVEAU EQ &&NIVEAU_GEO_NIC
-IF &&NIVEAU_GEO_NIC NE 1 OR 2 GOTO SKNG2;
WHERE VALEUR_AXE EQ 'GEOA971' OR 'GEOA972' OR 'GEOA973' OR 'GEOA974' OR 'GEOA975' OR 'GEOA976'
-GOTO SKNGEND
-SKNG2
-IF &&NIVEAU_GEO_NIC NE 3 GOTO SKNG3;
WHERE VALEUR_AXE EQ 'GEOA971R971' OR 'GEOA972R972' OR 'GEOA973R973' OR 'GEOA974R974' OR 'GEOA975R975' OR 'GEOA976R976'
-GOTO SKNGEND
-SKNG3
-IF &&NIVEAU_GEO_NIC NE 4 GOTO SKNG4;
WHERE VALEUR_AXE EQ 'GEOA971R971D971' OR 'GEOA972R972D972' OR 'GEOA973R973D973' OR 'GEOA974R974D974' OR 'GEOA975R975D975' OR 'GEOA976R976D976'
-GOTO SKNGEND
-SKNG4
-SKNGEND
ON TABLE HOLD AS TMP_AXE_GEO FORMAT ALPHA
END
-RUN
 
-SET &IORETURN=0;
-SET &CPT=0;
-SET &STR_GEO='';
-REPEAT :BOUCLE1 WHILE &IORETURN EQ 0;
-READ TMP_AXE_GEO &GEO_VAL.A15.
-IF &IORETURN NE 0 THEN GOTO :BOUCLE1;
-SET &STR_GEO=IF &STR_GEO EQ '' THEN &GEO_VAL ELSE IF &CPT EQ 1 THEN '''' | &STR_GEO | '''' | ',' | '''' | &GEO_VAL | '''' ELSE &STR_GEO | ',' | '''' | &GEO_VAL | '''';
-SET &STR_GEO=IF &LINES EQ 1 THEN '''' | &STR_GEO | '''' ELSE &STR_GEO;
-SET &CPT=&CPT+1;
-:BOUCLE1
-CLOSE TMP_AXE_GEO
 
 
 
-IF &DEPARTEMENT NE '_FOC_NULL' THEN GOTO LIST_DEPART;
-IF &REGION NE '_FOC_NULL' THEN GOTO LIST_REGION;
TABLE FILE LISTE_NREG
PRINT LIB_NREG
ON TABLE SAVE
END
-GOTO FIN_LIST
-LIST_REGION
TABLE FILE LISTE_REG
PRINT LIB_REG
ON TABLE SAVE
END
-GOTO FIN_LIST
 
-LIST_DEPART
TABLE FILE LISTE_DEPT
PRINT LIB_DEPT
ON TABLE SAVE
END
-GOTO FIN_LIST
-FIN_LIST
-RUN
 
 
-* Récupération du niveau d'activité sélectionné dans les filtres
-SET &NIVEAU_ACTI=IF (&SECTEUR NE '_FOC_NULL') AND (&TYPE_INT EQ '_FOC_NULL')  THEN 2 ELSE IF (&TYPE_INT NE '_FOC_NULL') AND (&MISSION EQ '_FOC_NULL') THEN 3 ELSE IF &MISSION NE '_FOC_NULL' THEN 4 ELSE 1;
-SET &NIV_ACTI_MOINS1= IF &NIVEAU_ACTI EQ 1 THEN 1 ELSE &NIVEAU_ACTI-1;
 
-SET &LIB_GEO_D_ACT=IF &NIVEAU_GEO_PLUS1 EQ 2 THEN 'Nb ETP Total nv régions' ELSE IF &NIVEAU_GEO_PLUS1 EQ 3 THEN 'Nb ETP Total régions' ELSE IF &NIVEAU_GEO_PLUS1 EQ 4 THEN 'Nb ETP Total départements' ELSE 'Nb ETP Total National';
-SET &NIVEAU_ACT_NUM=&NIVEAU_ACTI;
-SET &NIVEAU_GEO_NUM= &&NIVEAU_GEO_NIC;
 
-* Calcul du Dénominateur du ratio
 
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE/D20.1 AS 'ETPNAT'
-IF &NIVEAU_ACTI EQ 1 THEN GOTO :SKPBY0;
BY AX02_CODE
-:SKPBY0
-*Filtre INDICATEUR
WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
WHERE ( AX01_CODE EQ '2014' )
-*Filtre GEO
-IF &NIVEAU_ACTI EQ 1 THEN GOTO :SKPBY;
WHERE ( AX02_NIVEAU EQ &NIVEAU_GEO_PLUS1)
-GOTO :SKPBYEND
-:SKPBY
WHERE ( AX02_NIVEAU EQ 1)
 
-:SKPBYEND
-*Filtre ENTITE
WHERE ( AX03_CODE IN ( 'AARS16ENT1ARS00' ) )
 
-*Filtre ACTIVITE
-SET &NIVEAU_ACT_NAT= &NIVEAU_ACTI;
-IF &SECTEUR EQ '_FOC_NULL' GOTO WHTT_SECT;
-IF &NIVEAU_ACTI GT 2 GOTO WHTT_SECT;
-****** Vérification de l'axe corps(catégorie), si une catégorie est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui sélectionné dans le controle correspondant
-*******Vérification de l'axe corps(catégorie), si aucune catégorie n'est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui du niveau inférieur correspondant
-IF &STATUT NE '_FOC_NULL' THEN GOTO ACTI_NUMD;
WHERE ( AX04_CODE EQ 'AARS1600')
-SET &FILTRE_ACT_NAT= 'AARS1600';
 
-GOTO ACTI_NUMEND
-ACTI_NUMD
WHERE ( AX04_CODE EQ &SECTEUR)
-SET &FILTRE_ACT_NAT= &SECTEUR;
 
-GOTO FINWHEREACT;
-ACTI_NUMEND
 
 
-WHTT_SECT
-IF &TYPE_INT EQ '_FOC_NULL' GOTO WHTT_TYPINT;
-IF &NIVEAU_ACTI GT 3 GOTO WHTT_TYPINT;
-****** Vérification de l'axe corps(catégorie), si une catégorie est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui sélectionné dans le controle correspondant
-*******Vérification de l'axe corps(catégorie), si aucune catégorie n'est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui du niveau inférieur correspondant
-IF &STATUT NE '_FOC_NULL' THEN GOTO ACTI_NUMD1;
WHERE ( AX04_CODE EQ &SECTEUR)
-SET &FILTRE_ACT_NAT= &SECTEUR;
-GOTO ACTI_NUMEND1
-ACTI_NUMD1
WHERE ( AX04_CODE EQ &TYPE_INT)
-SET &FILTRE_ACT_NAT= &TYPE_INT;
-GOTO FINWHEREACT;
-ACTI_NUMEND1
 
 
-WHTT_TYPINT
-IF &MISSION EQ '_FOC_NULL' GOTO WHTT_MISS;
-IF &NIVEAU_ACTI GT 4 GOTO WHTT_MISS;
-****** Vérification de l'axe corps(catégorie), si une catégorie est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui sélectionné dans le controle correspondant
-*******Vérification de l'axe corps(catégorie), si aucune catégorie n'est sélectionnée, alors le filtre de l'activité du dénominateur doit être celui du niveau inférieur correspondant
-IF &STATUT NE '_FOC_NULL' THEN GOTO ACTI_NUMD2;
WHERE ( AX04_CODE EQ &TYPE_INT)
-SET &FILTRE_ACT_NAT= &TYPE_INT;
-GOTO ACTI_NUMEND2
-ACTI_NUMD2
WHERE ( AX04_CODE EQ &MISSION)
-SET &FILTRE_ACT_NAT= &MISSION;
-GOTO FINWHEREACT;
-ACTI_NUMEND2
 
 
-WHTT_MISS
WHERE ( AX04_NIVEAU EQ &NIV_ACTI_MOINS1)
-SET &NIVEAU_ACT_NAT= &NIV_ACTI_MOINS1;
-SET &FILTRE_ACT_NAT= &MISSION;
-FINWHEREACT
 
-*Filtre PROGRAMME DE FINANCEMENT
WHERE ( AX05_CODE IN ( 'AARS15EFF0TOUS ' ) )
-*Filtre STATUT
WHERE ( AX06_CODE IN ( 'AARS16ST00TOUT   ' ) )
-*Filtre INSPECTION CONTROLE
WHERE ( AX07_CODE IN ( 'AARS15ICE_0TOUT  ' ) )
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_NAT
END
-RUN
 
-*********SI on est au niveau 1 de l'axe ACTIVITE, alors on doit se positionner au niveau 1 de l'axe GEO, et récupérer la somme via une variable &ETPNAT, sinon
-*********on le conserve le résultat dans le tableau ETP_NAT
-IF &NIVEAU_ACTI NE 1 THEN GOTO SKIPREADNAT;
-READFILE ETP_NAT
-TYPE &ETPNAT
 
 
-GOTO RATIO
 
-SKIPREADNAT
-TYPE Niveau Activité sélectionnée
-*************** Calcul du numérateur du ratio******************************
 
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE/D20.1 AS 'ETPINTERMED'
BY AX02_CODE
BY AX02_NIVEAU
-*Filtre INDICATEUR
WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
WHERE ( AX01_CODE EQ '2014' )
-*Filtre GEO
WHERE ( AX02_NIVEAU EQ &NIVEAU_GEO_PLUS1)
-*Filtre ENTITE
WHERE ( AX03_CODE IN ( 'AARS16ENT1ARS00' ) )
-*Filtre ACTIVITE
-IF &SECTEUR EQ '_FOC_NULL' GOTO WHTT_SECT1;
-IF &NIVEAU_ACTI GT 2 GOTO WHTT_SECT1;
WHERE ( AX04_CODE EQ &SECTEUR)
-SET &FILTRE_ACT_NUM= &SECTEUR;
-WHTT_SECT1
-IF &TYPE_INT EQ '_FOC_NULL' GOTO WHTT_TYPINT1;
-IF &NIVEAU_ACTI GT 3 GOTO WHTT_TYPINT1;
WHERE ( AX04_CODE EQ &TYPE_INT)
-SET &FILTRE_ACT_NUM= &TYPE_INT;
-WHTT_TYPINT1
-IF &MISSION EQ '_FOC_NULL' GOTO WHTT_MISS1;
-IF &NIVEAU_ACTI GT 4 GOTO WHTT_MISS1;
WHERE ( AX04_CODE EQ &MISSION)
-SET &FILTRE_ACT_NUM= &MISSION;
-WHTT_MISS1
WHERE ( AX04_NIVEAU EQ &NIVEAU_ACTI)
-*Filtre PROGRAMME DE FINANCEMENT
WHERE ( AX05_CODE IN ( 'AARS15EFF0TOUS ' ) )
-*Filtre STATUT
-SET &STAT = IF &STATUT EQ '_FOC_NULL' THEN '''AARS16ST00TOUT''' ELSE &STATUT;
WHERE ( AX06_CODE EQ &STAT.EVAL)
-*Filtre STATUT
WHERE (AX07_CODE IN ('AARS15ICE_0TOUT'))
 
 
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_INTERMED
END
-RUN
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
 
-***************************Calcul du ratio ETP_INTERMED/ETP_NAT
 
-***************CAS 1: Le dénominateur est récupéré dans la variable &ETP_NAT
-* Lorsque le dénominateur fournit une seule ligne en sortie (Secteur=tous), calcul du ratio
-IF &NIVEAU_ACTI NE 1  THEN GOTO SKIPREADNAT2;
 
-RATIO
 
-TYPE CAS 1 Niveau Toutes les Activités
TABLE FILE VIEW_IND_VAL_IA
SUM VALEUR_FINALE/D20.1
COMPUTE ETPNAT/D8.2=&ETPNAT;
COMPUTE CALC_DP/D20.1= (VALEUR_FINALE /&ETPNAT) *100;
COMPUTE CALC_DPT/D8=CALC_DP;
BY AX02_CODE
BY AX02_NIVEAU
-*Filtre INDICATEUR
WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
-*Filtre TEMPS
WHERE ( AX01_CODE EQ '2014' )
-*Filtre GEO
WHERE ( AX02_CODE IN (&STR_GEO))
-*Filtre ENTITE
WHERE ( AX03_CODE IN ( 'AARS16ENT1ARS00' ) )
-*Filtre ACTIVITE et Filtre GEO
-IF &SECTEUR EQ '_FOC_NULL' GOTO WHTT_SECT12;
-IF &NIVEAU_ACTI GT 2 GOTO WHTT_SECT12;
WHERE ( AX04_CODE EQ &SECTEUR)
-SET &FILTRE_ACT_NUM= &SECTEUR;
-WHTT_SECT12
-IF &TYPE_INT EQ '_FOC_NULL' GOTO WHTT_TYPINT12;
-IF &NIVEAU_ACTI GT 3 GOTO WHTT_TYPINT12;
WHERE ( AX04_CODE EQ &TYPE_INT)
-SET &FILTRE_ACT_NUM= &TYPE_INT;
 
 
-WHTT_TYPINT12
-IF &MISSION EQ '_FOC_NULL' GOTO WHTT_MISS12;
-IF &NIVEAU_ACTI GT 4 GOTO WHTT_MISS12;
WHERE ( AX04_CODE EQ &MISSION)
-SET &FILTRE_ACT_NUM= &MISSION;
 
 
-WHTT_MISS12
WHERE ( AX04_NIVEAU EQ &NIVEAU_ACTI)
 
-*Filtre PROGRAMME DE FINANCEMENT
WHERE ( AX05_CODE IN ( 'AARS15EFF0TOUS ' ) )
 
-*Filtre STATUT
-SET &STAT = IF &STATUT EQ '_FOC_NULL' THEN '''AARS16ST00TOUT''' ELSE &STATUT;
WHERE ( AX06_CODE EQ &STAT.EVAL)
 
-*Filtre INSPECTION CONTROLE
WHERE ( AX07_CODE IN ('AARS15ICE_0TOUT'))
 
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_RATIO
END
-RUN
-IF &LINES EQ 0 THEN GOTO :ERRMESS;
 
 
-GOTO :TABLEFINALE;
 
-***************CAS 2: Le dénominateur est récupéré du tableau ETPNAT
-* Jointure entre les 2 tables numérateur et dénominateur pour effectuer les calcule en fonction des codes geo (axe 2)
-SKIPREADNAT2
 
-TYPE CAS 2 Niveau activité sélectionnée
-TYPE -- JOINTURE TABLE
 
JOIN CLEAR *
JOIN AX02_CODE IN ETP_INTERMED TO AX02_CODE IN ETP_NAT AS J1
END
-RUN
 
TABLE FILE ETP_INTERMED
SUM VALEUR_FINALE/D20.1 AS 'ETPINTERMED'
COMPUTE CALC_DP/D20.1=ETPINTERMED /ETPNAT *100;
COMPUTE CALC_DPT/D8=CALC_DP;
BY AX02_CODE
BY AX02_NIVEAU
BY ETPNAT
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_RATIO
END
-RUN
 
 
-GOTO :TABLEFINALE
 
 
-:TABLEFINALE
 
-* Calcul de la moyenne, de la médiane, du maximum et du minimum à partir des tables de ratio *******
-TYPE --- CALCUL MOYENNE / MEDIANE / MINI / MAXI ---
TABLE FILE ETP_RATIO
SUM
AVE.CALC_DPT WITHIN AX02_NIVEAU AS 'MOYENNE'
MDN.CALC_DPT WITHIN AX02_NIVEAU AS 'MEDIANE'
MAX.CALC_DPT WITHIN AX02_NIVEAU AS 'MAXI'
MIN.CALC_DPT WITHIN AX02_NIVEAU AS 'MINI'
BY AX02_NIVEAU
BY AX02_CODE
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS ETP_CALC
END
-RUN
 
JOIN CLEAR *
JOIN AX02_CODE IN ETP_RATIO TO AX02_CODE IN ETP_CALC AS J1
 
TABLE FILE ETP_RATIO
SUM
COMPUTE CALC_MOY/D8.1=MOYENNE; NOPRINT
COMPUTE CALC_MEDIAN/D8.1=MEDIANE; NOPRINT
COMPUTE DI_CALC/D3.1=(CALC_MOY - CALC_MEDIAN) / CALC_MEDIAN; NOPRINT
COMPUTE DIFF_CALC/A3= FTOA(DI_CALC,'(D3.1)', 'A3');
MINI/I3
MAXI/I3
BY AX02_CODE NOPRINT
WHERE RECORDLIMIT EQ 1
ON TABLE SET CENT-ZERO ON
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS TBL_RESULT FORMAT ALPHA
END
-RUN
-READ TBL_RESULT &DIFF_CALC.3. &MINI.3. &MAXI.3.
-CLOSE TBL_RESULT
 
-IF &DEPARTEMENT NE '_FOC_NULL' GOTO SCORE_DPT;
-IF &REGION NE '_FOC_NULL' GOTO SCORE_REG;
 
TABLE FILE AXE_GEOAR
PRINT GEOAR_N4
BY GEOAR_N2
WHERE GEOAR_N2 EQ 'GEOA971' OR 'GEOA972' OR 'GEOA973' OR 'GEOA974' OR 'GEOA975' OR 'GEOA976'
WHERE NIVEAU EQ 4
ON TABLE HOLD AS TABLE_DOMTOM
END
-RUN
 
-GOTO FIN_SCORE
-SCORE_REG
TABLE FILE AXE_GEOAR
PRINT GEOAR_N4
BY GEOAR_N3
WHERE GEOAR_N3 EQ 'GEOA971R971' OR 'GEOA972R972' OR 'GEOA973R973' OR 'GEOA974R974' OR 'GEOA975R975' OR 'GEOA976R976'
WHERE NIVEAU EQ 4
ON TABLE HOLD AS TABLE_DOMTOM
END
-RUN
 
-GOTO FIN_SCORE
-SCORE_DPT
 
TABLE FILE AXE_GEOAR
PRINT GEOAR_N4
BY GEOAR_N4
WHERE GEOAR_N4 EQ 'GEOA971R971D971' OR 'GEOA972R972D972' OR 'GEOA973R973D973' OR 'GEOA974R974D974' OR 'GEOA975R975D975' OR 'GEOA976R976D976'
WHERE NIVEAU EQ 4
ON TABLE HOLD AS TABLE_DOMTOM
END
-RUN
 
-FIN_SCORE
-RUN
 
 
-IF &DEPARTEMENT NE '_FOC_NULL' GOTO JOIN_DEPT;
-IF &REGION NE '_FOC_NULL' GOTO JOIN_REG;
JOIN AX02_CODE IN ETP_RATIO TO GEOAR_N2 IN TABLE_DOMTOM AS J5
-GOTO FIN_JOIN
-JOIN_REG
JOIN AX02_CODE IN ETP_RATIO TO GEOAR_N3 IN TABLE_DOMTOM AS J6
-GOTO FIN_JOIN
-JOIN_DEPT
JOIN AX02_CODE IN ETP_RATIO TO GEOAR_N4 IN TABLE_DOMTOM AS J7
-FIN_JOIN
 
TABLE FILE ETP_RATIO
PRINT
VALEUR_FINALE
CALC_DP
CALC_DPT
MOYENNE
MEDIANE
MINI
MAXI
BY GEOAR_N4
 
ON TABLE HOLD AS DPT_RATIO
END
-RUN
 
TABLE FILE LISTE_DEPT
PRINT LIB_DEPT
BY CODE_GEOAR
BY CODE_DEPT
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS SCORE FORMAT ALPHA
END
-RUN
 
JOIN CODE_GEOAR IN SCORE TO GEOAR_N4 IN DPT_RATIO AS J8
TABLE FILE SCORE
PRINT CALC_DPT
LIB_DEPT
BY CODE_DEPT
WHERE CODE_DEPT EQ '971' OR '972' OR '973' OR '974' OR '975' OR '976'
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS foccache/SCORE_DOMTOM FORMAT ALPHA
END
-RUN
 
-INCLUDE ars3_DETAIL_REGLES
-IF &EXCEL_LAUNCH='OUI' GOTO SKP_CARTO;
-HTMLFORM ars3_affiche_domtom
-RUN
-GOTO SKP_CARTO;
-:ERRMESS
-INCLUDE ars3_NODATA
-SKP_CARTO
