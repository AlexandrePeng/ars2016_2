-*============================================================================
-* Projet Suivi d'activité ARS 2014 - Tableau de bord contrôleur de gestion
-*
-* Destinataire  : Philippe Louvel
-* Auteur        : Nicolas Wattiaux (Information Builders France) / Guy Futé (Aixial BI)
-* Date création : 12/08/2015
-* Description   : Enquête Activité ARS par Statut
-*
-*----------------------------------------------------------------------------
-* Modifications
-*  Date   : jj/mm/ssaa
-*  Auteur : first last - sté
-*  Objet  : ...
-*============================================================================
-SET ECHO = ALL ;
-DEFAULTH &AX04_LIBELLE=''
-*SET CENT-ZERO Ajout du 0 avant les décimales lorque l'on a une valeur compris entre 0 et 1
SET CENT-ZERO = ON
 
-SET &ID_INDICATEUR = 'I_GIDB001C';
-INCLUDE ars3_ADHOC_DEFAUT
 
-*****************************************************************************
-* Definition du passage de paramètre
-*****************************************************************************
 
-*Filtre GEO
TABLE FILE AXE_GEOAR
PRINT VALEUR_AXE
WHERE NIVEAU EQ &GEO
-IF &GEO EQ '1' THEN GOTO :GEO_N1 ELSE IF &GEO EQ '2' THEN GOTO :GEO_N2 ELSE IF &GEO EQ '4' THEN GOTO :GEO_N4;
-:GEO_N4
WHERE GEOAR_N4 EQ &DEPARTEMENT
-:GEO_N2
WHERE GEOAR_N2 EQ &NEWREG
-:GEO_N1
WHERE GEOAR_N1 EQ &GEO_NATIONAL
 
ON TABLE HOLD AS TMP_AXE_GEOAR FORMAT ALPHA
END
 
 
-*Filtre entite
 
TABLE FILE AXE_AARS16ENT
PRINT VALEUR_AXE
-IF &ENTITE_TYPE EQ '_FOC_NULL' THEN GOTO :INSPEC_N1 ELSE GOTO :INSPEC_N2;
 
-:INSPEC_N2
WHERE AARS16ENT_N2 EQ &ENTITE_TYPE
-GOTO :FINENTITE
-:INSPEC_N1
WHERE VALEUR_AXE EQ &ENTITE_NATIONAL
-:FINENTITE
ON TABLE HOLD AS TMP_AXE_AARS16ENT FORMAT ALPHA
END
 
-*FILTRE ACTIVITE
 
TABLE FILE AXE_AARS16DOM
PRINT VALEUR_AXE
WHERE NIVEAU EQ &ACTIVITE
-IF &ACTIVITE EQ '1' THEN GOTO :ACTIVITE_N1 ELSE IF &ACTIVITE EQ '2' THEN GOTO :ACTIVITE_N2 ELSE IF &ACTIVITE EQ '3' THEN GOTO :ACTIVITE_N3 ELSE IF &ACTIVITE EQ '4' THEN GOTO :ACTIVITE_N4;
 
-:ACTIVITE_N4
WHERE AARS16DOM_N4 EQ &MISSION
 
-:ACTIVITE_N3
WHERE AARS16DOM_N3 EQ &ACTIVITE_TYP_INT
 
-:ACTIVITE_N2
WHERE AARS16DOM_N2 EQ &ACTIVITE_SECTEUR
 
-:ACTIVITE_N1
WHERE AARS16DOM_N1 EQ &ACTIVITE_NATIONAL
 
ON TABLE HOLD AS TMP_AXE_AARS16DOM FORMAT ALPHA
END
-RUN
-SET &NBACTIV=&LINES;
 
-*FILTRE STATUT/EMPLOI
TABLE FILE AXE_AARS16STATUT
PRINT VALEUR_AXE
WHERE NIVEAU EQ &STATUT_STATUT
-IF &STATUT_STATUT EQ '1' THEN GOTO :STATUT_N1 ELSE IF &STATUT_STATUT EQ '2' THEN GOTO :STATUT_N2 ELSE IF &STATUT_STATUT EQ '3' THEN GOTO :STATUT_N3 ;
-:STATUT_N3
WHERE AARS16STATUT_N3 EQ &EMPLOI
 
-:STATUT_N2
WHERE AARS16STATUT_N2 EQ &STATUT
 
-:STATUT_N1
WHERE AARS16STATUT_N1 EQ &STATUT_NATIONAL
 
ON TABLE HOLD AS TMP_AXE_AARS15STAT FORMAT ALPHA
END
 
 
-*Filtre Plafond
TABLE FILE AXE_AARS15EFF
PRINT VALEUR_AXE
-IF &PLAFOND EQ '_FOC_NULL' THEN GOTO :PLAFOND_N1 ELSE GOTO :PLAFOND_N2;
 
-:PLAFOND_N2
WHERE AARS15EFF_N2 EQ &PLAFOND.QUOTEDSTRING
-GOTO :FINPLAFOND
-:PLAFOND_N1
WHERE VALEUR_AXE EQ &PLAFOND_NATIONAL.QUOTEDSTRING
-:FINPLAFOND
ON TABLE HOLD AS TMP_AXE_AARS15EFF FORMAT ALPHA
END
 
 
-*Filtre Inspection contrôle
TABLE FILE AXE_AARS15ICE
PRINT VALEUR_AXE
-IF &INSPECT_CTRL EQ '_FOC_NULL' THEN GOTO :ICE_N1 ELSE GOTO :ICE_N2;
 
-:ICE_N2
WHERE AARS15ICE_N2 EQ &INSPECT_CTRL
-GOTO :FINICE
-:ICE_N1
WHERE VALEUR_AXE EQ &INSPECT_NATIONAL
-:FINICE
ON TABLE HOLD AS TMP_AXE_AARS15ICE FORMAT ALPHA
END
 
-RUN
 
-*****************************************************************************
-* Détermination de la règle de gestion correspondante
-*****************************************************************************
 
TABLE FILE AXE_AARS16DOM
BY AARS16DOM_N2
WHERE NIVEAU EQ &ACTIVITE
-IF &MISSION EQ '_FOC_NULL' GOTO SKVMISS;
WHERE AARS16DOM_N4 EQ &MISSION
-SKVMISS
-IF &ACTIVITE_TYP_INT EQ '_FOC_NULL' GOTO SKVTYPI;
WHERE AARS16DOM_N3 EQ &ACTIVITE_TYP_INT
-SKVTYPI
-IF &ACTIVITE_SECTEUR EQ '_FOC_NULL' GOTO SKVSECT;
WHERE AARS16DOM_N2 EQ &ACTIVITE_SECTEUR
-SKVSECT
ON TABLE HOLD AS VERIF_REGLE
END
-SET &REGLE_GESTION=IF (&LINES GT 1) AND (&ACTIVITE EQ 4) AND (&NBACTIV GT 1) THEN '3' ELSE IF (&LINES EQ 1) AND (&ACTIVITE EQ 4) AND (&NBACTIV GT 1) THEN '2'
- ELSE IF (&LINES EQ 1) AND (&ACTIVITE EQ 4) AND (&NBACTIV EQ 1) THEN '1' ELSE IF (&LINES GT 1) AND (&ACTIVITE EQ 3) AND (&NBACTIV GT 1) THEN '3'
- ELSE IF (&LINES EQ 1) AND (&ACTIVITE EQ 3) THEN '2' ELSE IF (&LINES GT 1) AND (&ACTIVITE EQ 2) THEN '3' ELSE IF (&LINES EQ 1) AND (&ACTIVITE EQ 2) THEN '2' ELSE '3';
-TYPE &REGLE_GESTION
 
 
 
 
-*****************************************************************************
-* Création du rapport contrôleur de gestion
-*****************************************************************************
 
TABLE FILE VIEW_IND_VAL_IA
 
PRINT
	VALEUR_FINALE AS VALEUR_ARS
	AX02_ORD
	AX03_ORD
	AX04_ORD
	AX05_ORD
	AX06_ORD
	AX07_ORD
 
BY AX02_CODE
BY AX03_CODE
BY AX04_CODE
BY AX05_CODE
BY AX06_CODE
BY AX07_CODE
 
-*Filtre INDICATEUR
WHERE ( ID_INDICATEUR EQ '&ID_INDICATEUR' )
 
-*Filtre TEMPS
	WHERE ( AX01_CODE EQ '&EXERCICE' )
 
-*Filtre GEO
	WHERE ( AX02_CODE EQ (TMP_AXE_GEOAR) )
 
-*Filtre ENTITE
	WHERE ( AX03_CODE EQ (TMP_AXE_AARS16ENT))
 
-*Filtre ACTIVITE
	WHERE ( AX04_CODE EQ (TMP_AXE_AARS16DOM) )
 
-*Filtre PLAFOND
	WHERE ( AX05_CODE EQ (TMP_AXE_AARS15EFF) )
 
-*Filtre STATUT
	WHERE ( AX06_CODE EQ (TMP_AXE_AARS15STAT) )
 
-*Filtre INSP_CONT
	WHERE ( AX07_CODE EQ (TMP_AXE_AARS15ICE) )
ON TABLE SET ASNAMES ON
ON TABLE HOLD AS TMP_FINAL FORMAT FOCUS INDEX AX02_CODE
 
END
 
-*Jointure
JOIN CLEAR *
JOIN
 AX02_CODE IN TMP_FINAL TO VALEUR_AXE IN AXE_GEOAR AS J1
END
JOIN
 AX03_CODE IN TMP_FINAL TO VALEUR_AXE IN AXE_AARS16ENT AS J2
END
JOIN
 AX04_CODE IN TMP_FINAL TO VALEUR_AXE IN AXE_AARS16DOM AS J3
END
JOIN
 AX05_CODE IN TMP_FINAL TO VALEUR_AXE IN AXE_AARS15EFF AS J4
END
JOIN
 AX06_CODE IN TMP_FINAL TO VALEUR_AXE IN AXE_AARS16STATUT AS J5
END
JOIN
 AX07_CODE IN TMP_FINAL TO VALEUR_AXE IN AXE_AARS15ICE AS J6
END
 
DEFINE FILE TMP_FINAL
CLE_CONC/A75=AX02_CODE || AX04_CODE;
END
 
TABLE FILE TMP_FINAL
PRINT
VALEUR_ARS
-*AXE GEO
    GEOAR_N1_LIB
	GEOAR_N2_LIB
	GEOAR_N3_LIB
	GEOAR_N4_LIB
-*AXE ENTITE
	AARS16ENT_N1_LIB
	AARS16ENT_N2_LIB
 
-*AXE ACTIVITE
	AARS16DOM_N1_LIB
	AARS16DOM_N2_LIB
	AARS16DOM_N3_LIB
	AARS16DOM_N4_LIB
 
-*AXE PLAFOND
	AARS15EFF_N1_LIB
	AARS15EFF_N2_LIB
 
-*Filtre STATUT
	AARS16STATUT_N1_LIB
	AARS16STATUT_N2_LIB
	AARS16STATUT_N3_LIB
 
-*Filtre inspection controle
	AARS15ICE_N1_LIB
	AARS15ICE_N2_LIB
 
-*Champs pour le tri
    GEOAR_N1_ORD
	GEOAR_N2_ORD
	GEOAR_N3_ORD
    GEOAR_N4_ORD
	AARS16ENT_N1_ORD
	AARS16ENT_N2_ORD
	AARS16DOM_N1_ORD
	AARS16DOM_N2_ORD
	AARS16DOM_N3_ORD
	AARS16DOM_N4_ORD
    AARS15EFF_N1_ORD
	AARS15EFF_N2_ORD
	AARS16STATUT_N1_ORD
	AARS16STATUT_N2_ORD
	AARS16STATUT_N3_ORD
 
	AARS15ICE_N1_ORD
	AARS15ICE_N2_ORD
 
 
    BY CLE_CONC
    BY AX02_ORD
	BY AX02_CODE
	BY AX03_ORD
	BY AX04_ORD
	BY AX05_ORD
	BY AX06_ORD
	BY AX07_ORD
-IF &INDUCTEUR EQ 'N' THEN GOTO :SAUT_HOLD;
ON TABLE HOLD AS TMP_INTER FORMAT ALPHA
-GOTO :FIN_HOLD;
-:SAUT_HOLD;
ON TABLE HOLD AS TMP_FINAL_EXCEL FORMAT ALPHA
END
-RUN
-GOTO :SAUTINDUCTS;
-:FIN_HOLD;
END
-RUN
 
DEFINE FILE VIEW_IND_VAL_IA
-*Dans le cas de la règle de gestion 1 la clé de jointure est la concaténation du code géo avec le code domaine d'activité
-IF &REGLE_GESTION NE '1' GOTO SKCLE1;
CLE_CONCAT/A75=AX02_CODE || AX03_CODE;
-GOTO ESKCLE
-SKCLE1
-*Dans le cas de la règle de gestion 2 ou 3 la clé de jointure est le code GEO correspondant
-IF &REGLE_GESTION NE '2' OR '3' GOTO SKCLE2;
CLE_CONCAT/A75=AX02_CODE;
-SKCLE2
-ESKCLE
END
TABLE FILE VIEW_IND_VAL_IA
PRINT AX04_LIBELLE VALEUR_FINALE
-*AX03_CODE AX03_LIBELLE
BY CLE_CONCAT
WHERE ID_INDICATEUR EQ 'I_GIDB0013'
WHERE ( AX02_CODE EQ (TMP_AXE_GEOAR) )
-IF &REGLE_GESTION NE '1' GOTO SKCLE10;
WHERE AX03_NIVEAU EQ 4
-GOTO ESKCLE1
-SKCLE10
-IF &REGLE_GESTION NE '2' GOTO SKCLE20;
WHERE AX03_NIVEAU EQ 2
-GOTO ESKCLE1
-SKCLE20
-IF &REGLE_GESTION NE '3' GOTO SKCLE30;
WHERE AX03_NIVEAU EQ 1
-SKCLE30
-ESKCLE1
ON TABLE HOLD AS TMP_INDUC FORMAT ALPHA
END
-RUN
-READFILE TMP_INDUC
 
-IF &REGLE_GESTION NE '1' GOTO SKCLE11;
JOIN CLE_CONCAT IN TMP_INDUC TO ALL CLE_CONC IN TMP_INTER AS J7
-SKCLE11
-IF &REGLE_GESTION NE '2' OR '3' GOTO SKCLE21;
JOIN CLE_CONCAT IN TMP_INDUC TO ALL AX02_CODE IN TMP_INTER AS J7
-SKCLE21
 
 
TABLE FILE TMP_INDUC
 
PRINT
AX04_LIBELLE
VALEUR_FINALE
VALEUR_ARS
-*AXE GEO
    GEOAR_N1_LIB
	GEOAR_N2_LIB
	GEOAR_N3_LIB
	GEOAR_N4_LIB
-*AXE ENTITE
	AARS16ENT_N1_LIB
	AARS16ENT_N2_LIB
 
-*AXE ACTIVITE
	AARS16DOM_N1_LIB
	AARS16DOM_N2_LIB
	AARS16DOM_N3_LIB
	AARS16DOM_N4_LIB
 
-*AXE PLAFOND
	AARS15EFF_N1_LIB
	AARS15EFF_N2_LIB
 
-*Filtre STATUT
	AARS16STATUT_N1_LIB
	AARS16STATUT_N2_LIB
	AARS16STATUT_N3_LIB
 
-*Filtre inspection controle
	AARS15ICE_N1_LIB
	AARS15ICE_N2_LIB
 
-*Champs pour le tri
    GEOAR_N1_ORD
	GEOAR_N2_ORD
	GEOAR_N3_ORD
    GEOAR_N4_ORD
	AARS16ENT_N1_ORD
	AARS16ENT_N2_ORD
	AARS16DOM_N1_ORD
	AARS16DOM_N2_ORD
	AARS16DOM_N3_ORD
	AARS16DOM_N4_ORD
    AARS15EFF_N1_ORD
	AARS15EFF_N2_ORD
	AARS16STATUT_N1_ORD
	AARS16STATUT_N2_ORD
	AARS16STATUT_N3_ORD
 
	AARS15ICE_N1_ORD
	AARS15ICE_N2_ORD
 
 
    BY AX02_ORD NOPRINT
	BY AX03_ORD NOPRINT
	BY AX04_ORD NOPRINT
	BY AX05_ORD NOPRINT
	BY AX06_ORD NOPRINT
	BY AX07_ORD NOPRINT
 
ON TABLE HOLD AS TMP_FINAL_EXCEL
 
END
 
 
 
-*Test pour vérifier que les données puissent être intégrer en sortie EXCEL
 
-:SAUTINDUCTS;
-IF &LINES EQ 0 THEN GOTO :ERRMESS ELSE IF &LINES GT 65536 GOTO :ERREXCEL;
 
 
TABLE FILE TMP_FINAL_EXCEL
 
HEADING
"Enquête Activité ARS par Statut - ETP"
""
FOOTING
""
"Source DFAS / SDC2G / PCG - Siperf"
"Date d'impression: &DATEDMYY "
 
SUM
 VALEUR_ARS AS 'ETP'
 
-*AXE GEO
	BY 	GEOAR_N1_ORD NOPRINT
	BY GEOAR_N1_LIB AS 'Pays'
-IF &GEO EQ 1 THEN GOTO :ENDLIBGEO;
	BY	GEOAR_N2_ORD NOPRINT
	BY GEOAR_N2_LIB AS 'Nouvelles Régions'
-IF &GEO EQ 2 THEN GOTO :ENDLIBGEO;
	BY	GEOAR_N3_ORD NOPRINT
	BY GEOAR_N3_LIB AS 'Régions'
-IF &GEO EQ 3 THEN GOTO :ENDLIBGEO;
	BY	GEOAR_N4_ORD NOPRINT
	BY GEOAR_N4_LIB AS 'Départements'
-:ENDLIBGEO
-*AXE ENTITE
	BY AARS16ENT_N1_ORD NOPRINT
	BY AARS16ENT_N1_LIB AS 'Entité'
-IF &ENTITE_TYPE EQ '_FOC_NULL' THEN GOTO :ENDLIBENTITE;
	BY AARS16ENT_N2_ORD NOPRINT
	BY AARS16ENT_N2_LIB AS 'Siège et/ou DT'
-:ENDLIBENTITE
 
-*AXE ACTIVITE
-IF &ACTIVITE EQ 1 THEN GOTO :ENDLIBACTIVITE;
	-*BY AARS13DOM_N1_ORD NOPRINT
	-*BY AARS13DOM_N1_LIB AS 'Activité'
	BY AARS16DOM_N2_ORD NOPRINT
	BY AARS16DOM_N2_LIB AS 'Secteur'
-IF &ACTIVITE EQ 2 THEN GOTO :ENDLIBACTIVITE;
	BY AARS16DOM_N3_ORD NOPRINT
	BY AARS16DOM_N3_LIB AS 'Type d'intervention'
-IF &ACTIVITE EQ 3 THEN GOTO :ENDLIBACTIVITE;
	BY AARS16DOM_N4_ORD NOPRINT
	BY AARS16DOM_N4_LIB AS 'Mission'
-:ENDLIBACTIVITE;
-IF &INDUCTEUR EQ 'N' GOTO SKAFFINDUC;
BY VALEUR_FINALE AS '&AX04_LIBELLE'
-SKAFFINDUC
-*Filtre Plafond
-IF &PLAFOND EQ '_FOC_NULL' GOTO SKPPLAF;
	ACROSS AARS15EFF_N2_ORD NOPRINT
	ACROSS AARS15EFF_N2_LIB AS 'Plafond / Hors Plafond'
-SKPPLAF
-*Filtre STATUT
-IF &STATUT_STATUT EQ '1' THEN GOTO :ENDLIBSTATUT;
	ACROSS AARS16STATUT_N2_ORD NOPRINT
	ACROSS AARS16STATUT_N2_LIB AS 'Statut'
-IF &STATUT_STATUT EQ '2' THEN GOTO :ENDLIBSTATUT;
	ACROSS AARS16STATUT_N3_ORD NOPRINT
	ACROSS AARS16STATUT_N3_LIB AS 'Emploi/corps'
-:ENDLIBSTATUT;
 
-*Filtre Inspection controle
-IF &INSPECT_CTRL EQ '_FOC_NULL' GOTO SKPINSPC;
	BY AARS15ICE_N2_ORD NOPRINT
	BY AARS15ICE_N2_LIB AS 'Inspection Contrôle'
-SKPINSPC
 
-IF &PRESENTATION EQ '1' THEN GOTO :PREZ_TABLEAU ELSE IF &PRESENTATION EQ '2' GOTO :PREZ_BDD;
 
-*Présentation Tableau :
-:PREZ_TABLEAU
ON TABLE SET BYDISPLAY OFF
-GOTO :ENDPREZ
 
-*Présentation BDD:
-:PREZ_BDD
ON TABLE SET BYDISPLAY ON
-:ENDPREZ
 
-IF &STATUT EQ 1 THEN GOTO :NOROWTOTAL;
ON TABLE ROW-TOTAL AS 'TOTAL'
-:NOROWTOTAL
 
ON TABLE COLUMN-TOTAL AS 'TOTAL'
 
ON TABLE PCHOLD FORMAT EXL2K
 
ON TABLE SET STYLE *
TYPE=FOOTING,JUSTIFY=LEFT,STYLE=BOLD+ITALIC,SIZE=12,COLOR=RGB(72 86 115),FONT=ARIAL,$
TYPE=HEADING,LINE=1,JUSTIFY=CENTER,STYLE=BOLD,SIZE=14,COLOR=RGB(72 86 115),FONT=ARIAL,$
TYPE=HEADING,LINE=2,JUSTIFY=CENTER,STYLE=BOLD,SIZE=12,COLOR=RGB(72 86 115),FONT=ARIAL,$
UNITS=IN,TOPMARGIN=0.000000,BOTTOMMARGIN=0.000000,
SQUEEZE=ON,WRAP=ON,ORIENTATION=LANDSCAPE,$
TYPE=REPORT,FONT='ARIAL',SIZE=10,COLOR='BLACK',BACKCOLOR='NONE',STYLE=NORMAL,$
TYPE=TITLE,TOPGAP=0, BOTTOMGAP=0,STYLE=BOLD,JUSTIFY= CENTER,GRID=OFF,$
ENDSTYLE
 
END
-GOTO :FINI
-:ERRMESS
-HTMLFORM BEGIN
<TD>Message d'information<BR>
<BR>
<I><B>Cette requête n'a pas retourné de données.<BR>
</I></B></TD>
-HTMLFORM END
-GOTO :FINI
-:ERREXCEL
-HTMLFORM BEGIN
<TD>Message d'information<BR>
<BR>
<I><B>Cette requête retourne trop de données pour être affichée sous Excel.<BR>
</I></B></TD>
-HTMLFORM END
-:FINI
 
